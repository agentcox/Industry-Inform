"Industry" by Charles Cox


A thing can be examined or unexamined.

Use no scoring.
Use undo prevention.

Carry out examining something: 
    now the noun is examined.

Price is a kind of value. $-1.99 specifies a price with parts dollars and cents (optional, preamble optional). 



Part 1 - Behind Glass

Chapter 1 - Armistice Day

Armistice Day is a Scene.
Armistice Day ends when The Departure begins.
Armistice Day begins when play begins.
When play begins: 
    say "You're dead. You have to be.

Fifty-one hours and an eyeblink ago you stood in front of a crowd of fifty-thousand outside the Gas Temple and made it clear that nobody was going to break you, your company, your shareholders, or the spirit of a people.

A minute later, you were assassinated.

At least, that's how it was supposed to go. You're supposed to be dead. Buried under a mountain of gray California dirt. So why does it feel like you're still alive?"

The Bastille is a region.

A person can be healthy or incapacitated. The player is healthy.

[the broken leg]
A broken leg is a thing. A broken leg is fixed in place. The description is "Painfully, you raise up your pant cuff and find a splint of white bone jutting out from the front of your left leg. Sickened, you lower the cuff and try to forget both the image and the pain, and have little luck with either." 
 
The player is carrying a broken leg.
Understand "left leg" as leg.

Instead of dropping broken leg, say "You can't drop that. It's your leg and you'll have to deal with it."

When Armistice Day Begins, say "Woah."

[yelling]
Yelling is an action applying to nothing.
Understand "yell" as yelling.
Understand "shout" as yelling.
Understand "cry" as yelling.
Understand "call out" as yelling.
Understand "call for help" as yelling.

Carry out yelling:
	say "You yell out into the distance, hoping for a reply. No reply comes."

Every turn while Armistice Day is happening:
If the player has the broken leg for 2 turns, say "You feel a pain in your left leg.";
If the player has the broken leg for 4 turns, say "You're growing increasingly aware of the pain in your left leg.";
If the player has the broken leg for 9 turns, say "The pain in your leg is excruciating. You're starting to feel sick. You're going to need medical help soon.";
If the player has the broken leg for 20 turns, say "You realize that your leg is still bleeding. You feel faint. You've got to go for help."

[looking at self]
Instead of examining yourself during Armistice Day, say "It's hard to tell what's happened to you strictly by looking at your clothes. You've got a suit on - one of your favorites, and the one you wore to the Gas Temple - but no tie. Your suitcuffs are clean, but your shirtcuffs have suspicious rusty stains. Your hands look scuffed up, but okay.

Below the waist, however, there's a problem. Something is wrong with your left leg."

[the splinted leg]
A splinted leg is a thing. A splinted leg is fixed in place. The description is "Your left leg is wrapped by thirty feet of polyvinyl fire hose covering a makeshift splint. " 

Instead of dropping splinted leg, say "You can't drop that. It's your leg and you'll have to deal with it."

[the staircase]
The Forty-Fourth Floor Staircase is a room in The Bastille. "[if unvisited]You are against the wall of a concrete staircase, cold and pourous. [end if]Through red emergency lighting, you can see a large stenciled number on the wall, indicating the forty-fourth floor. Somewhere, drifting in the echo of the concrete, you can hear the muffled sounds of fireworks, celebrating Armistice Day.

The stairs that lead up terminate at a concrete support, in which an entryway is just barely visible. Downward, the staircase leads into a dark black fog."

The Forty-Third floor Entry is in the Bastille. It is below the Staircase.

Instead of going up from the staircase when the Player is carrying a broken leg, say "When you try to stand and move up the staircase, you feel a searing pain in your left leg. You sink back against the wall, realizing there's no way you can move unless you support yourself somehow."

Instead of going down from the staircase when the Player is carrying a broken leg, say "When you try to stand and move down the staircase, you feel a searing pain in your left leg. You sink back against the wall, realizing there's no way you can move unless you support yourself somehow."

Instead of going down from the staircase when the Player is carrying a splinted leg, say "You try to follow the path of the extinguisher foam downstairs, but going down is a lot harder than going up with this splinted leg, especially with the handrail out of action. You'll have to find another way."

 
The wall is a backdrop in the staircase. "The wall glows red under a bank of emergency lights. The stenciled digits indicating the forty-forth floor are painted in a luminescent shade that glows an unnatural blue. The phrase 'Bastille Bldg.' is stenciled below."

The support is a backdrop in the staircase. "You count ten steps from the landing to a bulky concrete support into which you can see the outline of an entryway."

The entryway is a backdrop in the staircase. "The entryway at the top of the stairs is just barely visible by its outline against the concrete support. It looks open. It is up the stairs from your current location."


Understand "number" as wall when the player is in the staircase.
Understand "numbers" as wall when the player is in the staircase.
Understand "upstairs" as up when the player is in the staircase.
Understand "up stairs" as up when the player is in the staircase.


The handrail is in the staircase. "A handrail twists down the near edge of the staircase." It is fixed in place. The description is "The handrail going down the staircase wall has been splintered off near the landing."

The slat is a thing. "A wooden slat sits on the ground." The description is "This slat comes from the handrail on the forty-fourth floor staircase of the Bastille Building. It is longer than it is wide, having split lengthwise from the rest of the rail. It looks fairly sturdy."


The fire locker is a container in the staircase. It is fixed in place. It is openable. It is closed. "There is a fire locker on the wall." The description is "The locker sits just under the numbers on the wall.

[if closed]Aha! The locker looks sheared off a bit on one edge, the resultant hole revealing a glint of metal inside. [otherwise] You've opened up a hole in the side of the locker."

The extinguisher is inside the locker. The description is "This bulky, heavy industrial extinguisher looks more like a welding tank, but fat, with reinforced edges whose bubbling paint hints at an advanced number of years. You can't quite figure out how it's supposed to work."

The fire hose is inside the locker. The description is "The hose is wrapped around a spool. It looks about thirty feet long, topped with a brass nozzle."

Before taking the hose, say "You yank on the hose, spinning it off the spool."

Instead of opening the locker when the locker is unexamined, say "The door won't open. There's a key lock that looks frozen in place."

Before opening the locker when the locker is examined, say "You pull the rest of the metal away from the shear to make an opening - you're pretty sure your hand can reach the lock from the inside now."

Instead of examining the locker when the locker is examined, say "You think you can get your hand inside the locker through the shear. You should be able to open it this way."

Understand "hole" as locker when the player is in the staircase.
Understand "shear" as locker when the player is in the staircase.

Instead of attacking the locker, say "You don't want to break your hands against the metal locker, and you can't manage a kick that high with your leg in the shape it's in."

[supporting something]
Supporting it with is an action applying to two things.

Understand "support [something] with [something]" as supporting it with.
Understand "fix [something] with [something]" as supporting it with.
Understand "bandage [something] with [something]" as supporting it with.
Understand "splint [something] with [something]" as supporting it with.
Understand "repair [something] with [something]" as supporting it with.
Understand "fix [something]" as supporting it with.

Understand "tie [something] to [something]" as supporting it with.

Instead of supporting the leg with the leg:
	say "You'll need to fix it with something."
	
Instead of supporting the leg with the handrail:
	say "You try to find a way to patch up your leg with the [second noun], but you can't get it to work.";

Instead of supporting the hose with the leg:
	try supporting the leg with the hose.
	
Instead of supporting the slat with the leg:
	try supporting the leg with the slat.

Instead of supporting the leg with something (called the supporter):
	if the supporter is not the slat and the supporter is not the hose:
		say "You try to find a way to patch up your leg with the [supporter], but you can't get it to work.";
	otherwise if the supporter is the slat and the player is not carrying the hose:
		say "You put the slat against your leg but you can't keep it there - you need to secure it with something.";
	otherwise if the supporter is the hose and the player is not carrying the slat:
		say "You try to wrap your leg with the hose, but the hose doesn't provide enough support - you'll need something sturdy to brace it against.";
	otherwise:
		say "This just might work - you put the slat against your leg, locking your knee in place.
		
		
		While holding it down, you wrap the fire hose around your leg and the slat. It hurts - you can already see blood oozing out from the flaps of the hose.
		
		
		You manage to get the wraps all the way around. You can't find anywhere to fit the nozzle - it drapes clumsily from your leg - but otherwise it's not a bad job. You're standing, at least: wobbly, but standing.";
		remove the slat from play;
		remove the hose from play;
		remove the broken leg from play;
		now the player is carrying the splinted leg;

[hitting with fists]
Understand "break [something]" as attacking.
Understand "punch [something]" as attacking.
Understand "kick [something]" as attacking.

[hitting with an object]
Hitting it with is an action applying to two things.

Understand "hit [something] with [something]" as hitting it with.

Report hitting it with:
say "You slam the [second noun] into the [noun], but nothing gives."

[attacking the handrail]
Before attacking the handrail, instead say "The handrail looks ready to give, but hitting it with your hand isn't doing enough. You can't get your good leg up high enough to kick it, either."

Instead of hitting the handrail with the extinguisher for the first time, instead say "You raise up the extinguisher and bring it down hard on the handrail.

You hear the wood crack and give a little."

Instead of hitting the handrail with the extinguisher for the second time, instead say "You raise up the extinguisher again, groaning with the effort, and bring it down on the rail.

The wooden rail gives out a loud report. You can see an ugly crack forming lengthwise along the grain of the wood."

Instead of hitting the handrail with the extinguisher for the third time:
say "You can barely lift up the extinguisher for a third time. With shaking arms, you raise it up and bring it down on the rail as hard as you can.

With a lound bang, everything goes white.

You are covered in aqueous foam. The extinguisher has shot off somewhere down into the lower floors, no doubt self-propelled by the foam that's now pouring out of it at high velocity.

But, the job has been done - you notice that the handrail has split and left a long slat within reach.";
remove the extinguisher from play; move the slat to the Forty-Fourth Floor Staircase; change the description of the handrail to "The handrail leading downward has been split off, leaving a ragged set of splinters."; Change the initial appearance of the handrail to "A broken handrail trails downstairs."

Instead of taking the handrail when the slat is in the staircase:
	say "You see that the handrail has split and left a slat on the floor. You pick it up.";
	try taking the slat.

Before going up from the Staircase when the Breezeway is unvisited and the player is carrying a splinted leg, say "You fix your sights on the ten steps in front of you between here and the door - you realize the only way you're going to make it is to charge the stairs before your makeshift splint gives out. You take a breath, and go."

The Forty-Fifth Floor Breezeway is in the Bastille. It is above the Staircase. "[if unvisited]As you crash the fire door at the top of the stairs, you are hit by an autumn squall - you've made it out of the building onto a breezeway, and the wind up here is at storm velocity. [end if]The breezeway is a high modern arch, cresting over a series of transmission spikes glowing a dull red against the driving rain. It extends north toward a small observation annex.

Through the dark gray trunks of concrete on the breezeway, you can see far-flung fireworks exploding in the distance over the city.

A door leads back down to the staircase landing on the forty-fourth floor."

Instead of going down from the Breezeway when the Player is carrying a splinted leg, say "You look at the staircase leading back down to the landing, and your splinted leg throbs in sympathetic pain, oozing another blot of blood onto your bandage. The pain and the blood convince you that more vertical movement is too risky right now."

The Breezeway Service Gate is east of the Breezeway. The Breezeway Service Gate is a door. It is locked. " A service gate marked by a sign is to the east and looks to lead underneath the breezeway structure." The description is "The gate is a typical chain-link job ringed with capped pipe. Fence extends out on both sides. A sign on the Breezeway side says 'MAINTENANCE ONLY'. [if locked]There's a fat ceramic lock hanging heavily from the hook latch."

The spikes is a backdrop in the breezeway. "The spikes extend far above your head, terminating at red bulbs that pulse oddly against the storm's whipping wind and rain. These spikes are probably carrying ORBDIS, MARSAT, and PAXET loads from the Pacific distribution trunk, part of the weekly data commerce of the City of Industry."
The city is a backdrop in the breezeway. "The city is a shaky datagraph, resembling a crumbling set of bar plots on uneven lines. For any amount of decay the buildings still have gravitas for being tall and stable in this region - the earthquakes have gotten far worse in the last ten years. Hard to believe nearly a third of them are empty."
The fireworks is a backdrop in the breezeway. "Despite the lack of an actual culture, The City of Industry celebrates tonight. The Armistice Day fireworks are ionic in touch, crystalline arrangements of microexplosives nearly perfect in their actuary-inspired precision. Sadly, nature intervenes: the gale-force winds drag the explosions and smoke unceasingly eastward over the basin, smearing them artlessly across the sky."
The fence is a backdrop in the breezeway. "The fence supports the gate on both sides. It looks to be about eight feet high overall, and of the same chain-link construction as the gate. Through the fence you can see a small mesh steel walkway suspended under the breezeway."

The lock is a backdrop in the breezeway. "The lock is big and ribbed with steel shims. It'd take a lot to get this off."

Instead of climbing the Gate when the player is in the Breezeway and the player has the splinted leg, say "You take a look at the fence, and then at your leg - still broken, crudely splinted, and leaking blood - and realize that there's no way. All hope of getting over the fence is lost with your leg being the way it is."

Instead of climbing the fence when the player is in the breezeway and the player has the splinted leg, try climbing the gate.

The Service Undercarriage is in The Bastille. It is east of the Breezeway Service Gate. 

The Observation Annex is in the Bastille. It is north of the Breezeway. "The observation annex of the Bastille Building is a mollusk-like protrusion off the north end of the building, comprising a large, coif-like curved wind scoop and an offset crescentform ledge where you stand now, looking out over the city, sheltered from the wind and rain by a glass shield."

The plaque is a thing in the annex. "[if plaque is unexamined]You can see your reflection in a small but polished plaque that is mounted on the observation shield. Something is odd about your reflection.[otherwise]Your one-eyed face stares back at you from the burnished metal of the plaque.[end if]". the description is "You approach the glass wall that separates you from the city, hoping to get a glimpse of yourself - something more than your clothes and your hands - in the plaque that is affixed there.

You stop short.

Something is wrong with your face."

The shield is a backdrop in the annex.
The reflection is a backdrop in the annex.

The my face is a backdrop in the annex. "As you inch up to the glass to see yourself better, you get a twisting feeling in your stomach - instead of a familiar blue and white eyeball staring back at you from the right side of your face, there's only a hollow, ink-black pit.

Your right eye is gone. It is completely gone. Not even a trickle of blood traces back to the hole.". Understand "your face" as my face.

After examining the face for the first time:
say "You stumble, faint and dead-sick at the sight. They've taken your eye - someone carved it out of your head. As your remaining eye focuses on the plaque:

[fixed letter spacing]BASTILLE BUILDING CA. 2121[line break]'PLANT, AND REAP THE BOUNTY OF EARTH'[line break]COMMITTED TO US THIS ARMISTICE DAY BY:[variable letter spacing]

You realize that your name is inscribed below.

This was your building, those many years ago.

So many years ago.

You fall to the concrete in pain and shock.

Somewhere out there, you can hear more fireworks exploding against the cityscape as the world fades, being sucked down through the hole where your eye used to be, draining into your brain... 

";
now the player is in The Superego.

Instead of examining yourself when the player is in the annex, try examining the plaque.
Instead of examining reflection when the player is in the annex, try examining the plaque.

Instead of doing anything other than looking when the player is in the Superego:
	instead say "You can't. You don't have the strength right now."
	
Instead of examining yourself when the player is in the Superego:
	instead say "You can't see anything."
	

The Selfimage is a region.
The Superego is a room in the Selfimage. "It's cold here."

Every turn:
	if the player is in the superego for one turn:
		say "...life always looked like a straight line - especially back when they looked at the City of Industry and they saw salvation...
		
		...then came the day the world split into two choices; you realized there were people with more at stake than you anticipated...";
	otherwise if the player is in the superego for two turns:
		say "...everybody knew you - everybody loved you - everybody asked for your opinion, your consideration in reforming the Industry, the new Economy...
		
		...and still they came. At first, you placated them; treated them like any other interested party wanting a handout; a cop at a border crossing...";
	otherwise if the player is in the superego for three turns:
		say "...but they were unlike anyone you had ever met. They simply refused to melt into the rest of the Industry fog...

		...their words started to feel like your words, yours like theirs, and what you could only forecast, they seemed to know...";
	otherwise if the player is in the superego for four turns:
		say "...but you had the last act already written...";
		say "...how could they do this to you?";
		now the player is in the Departure Lounge;
		remove the splinted leg from play.
		

[magic word for going to chapter 2]
Understand "xyzzy" or "say xyzzy" or "cast xyzzy" as casting xyzzy.
Casting xyzzy is an action applying to nothing.
Check casting xyzzy: 
	if The Gunfight is happening:
		say "You're already as far as this word will take you." instead;
		

Carry out casting xyzzy:
	if Armistice Day is happening:
		now the player is in the Departure Lounge;
	otherwise if The Departure is happening:
		now the player is carrying the prosthetic eye;
		now the player is carrying the film;
		now the player is carrying the boarding pass;
		now the agent is carrying the ticket;
		now the player is in the restroom;
	otherwise if The Orbflight scene is happening:
		now the player is in the PAXET Trunk.

Understand "xyzzq" or "say xyzzq" or "cast xyzzq" as casting xyzzq.
Casting xyzzq is an action applying to nothing.
Check casting xyzzq: 
	if The Gunfight is happening:
		say "You're already as far as this word will take you." instead;
		

Carry out casting xyzzq:
	now the player is in Argot Lounge.
		
Chapter 2 - The Departure

The Departure is a scene. The Departure begins when the player is in the Departure Lounge for the first time. The Departure ends when The Gunfight ends.

When The Departure begins:
	now the player is wearing the flex cast;
	now the player is wearing the tie;	
	now the player is wearing the eye;
	now the player is carrying the envelope;
	now the player is carrying the logostick;
[	the orbflight drops in a random number between 20 and 30 turns from now;]
	the orbflight drops in 20 turns from now;
		
Talking to is an action applying to one visible thing.

A thing can be activated or deactivated. A thing is usually deactivated.

Understand "talk to [something]" as talking.

Carry out talking to:
	if the noun is not a person:
		say "You try to engage the [noun] in conversation, but the [noun] is not having any of it.";
	otherwise:
		say "[The noun] ignores you.".

[the intercom]
At the time when the intercom calls:
	if The Gunfight is not happening:
		say "You hear the announcement sound for the intercom system. You realize they're calling for you.
	
	Gate Six. You're supposed to be at Gate Six.";
		the intercom calls in six turns from now.
	
[the orbflight]
At the time when the orbflight drops:
	if number of moves from the location to Gate 6 is less than 3:
		say "A low rumbling from somewhere outside draws your attention to the south windows of the departure lounge. You watch through the glass, and through and a haze of precautionary fire retardant glowing red from emergency lighting.
		
		
		A large mesh hoop is being drawn up to a forty-five degree angle out on the tarmac.
		
		
		The rumble grows, expands, splitting the air, layer after layer of fracturing, crackling screams. You see some of the travelers opening their mouths, stuffing their earphones deeper in their ears.
		
		
		The windows flex, pulse, vibrate. The sound grows unbearably loud - in an instant, there is a final whipcrack and the hoop is flexed to bursting, cradling a steaming, charred dart the size of a school bus.";
	otherwise:
		say "A low rumbling is coming from the direction of the south end of the departure lounge.
		
		
		You stop - listening for some nuance in the sound, but it merely grows, building on itself, turning into layer after layer of piercing, ripping shrieks that rattle the floor, the walls, and your head.
		
		
		As it reaches a crescendo, you clap your hands over your ears just in time for it to drop completely away with a final slap of pressure, ebbing away to leave a cottony residue of still air around you.";
	say "
	
	A voice comes over the lounge intercom: 'Whitmore Executive announces the arrival of flight 2A2, with service to Alexandria, Virginia, United States. This flight is now boarding at Gate Six.";
	now the agent is ready;
	the intercom calls in 15 turns from now.

[the envelope]
The envelope is a container that is openable. It is closed. The description is "The envelope is a long, formal, cream-colored packet with your name written in longhand on the front."

[the logostick]
The logostick is a thing. The description is "It's your stick - the combined identification, location processor and microcomputer developed by LogoS Corporation to replace the standard hyperchipped State Department folio assigned to citizens. It's about the size of a cigarette, but a little heavier on the held end."

[the ticket]
The ticket is a thing. It is in the envelope. The description is "The ticket is issued by ORBGROUP for a single-passenger, one way SubFlight - it says:

[fixed letter spacing]FLIGHT 2A2 JO'BG - AX'DA 0815[variable letter spacing]

A hand-written number 6 has been circled vigorously on the ticket."
[the note]
The letter is a thing. It is in the envelope. The description is "The letter is a typed memo with the letterhead of a company: LogoS. You know them well, and their President, Jonathan French.[line break]

It is his signature that completes the following letter:[line break]

My Friend - [line break]

Rare in my experience is a man who faces a set of circumstances such as you face now.[line break]

If you don't yet understand, you can trust me at least to help you see more clearly; LogoS is nothing if not the first to know in matters relational, and you can be assured: we know who mutilated you, who is to be blamed for your near-death in the City of Industry.[line break]

Understand, though: information cuts both ways, and we know the ruse you were playing as part of your Gas Temple campaign. Your deception was interrupted, and while not entirely exposed, your intentions were made clear to a few. We therefore know something about you, as well as your assailants.[line break]

To be sure, this would indicate two markers of advantage to us and none to you, but as I hinted earlier, your circumstances are exceptional. There is a service you can do for us, and to that end, you have received both an expedient exfiltration from the City, far enough away to hide our tracks, and as much medical care as we could administer enroute, so that you may be well enough to join us at our corporate center in Alexandria.[line break]

You have the necessary ticket for a suborbital flight on your person and my very best wishes for a safe and expedient trip up and over. Certainly, if you choose not to hear us out, we can't be blamed for any leakages of your personal affront to your own shareholders, let alone the law. Acher and The Priests may be old and slow, but a disgruntled market will shoulder them along to your door without much delay.[line break]

I add only that though our efforts have been both expedient and clandestine, there will be others with much at stake asking your whereabouts, and who may be in your midst now despite our precautions. If you are to act, be swift. You are by no means out of danger yet.[line break]

I remain yours very truly,[line break]

Jonathan French[line break]President, LogoS Board of Directors

"

[looking at yourself]
Instead of examining yourself for the first time during The Departure:
	if the player is in the restroom:
		try examining the mirror;
	otherwise:		
		say "You're not wearing what you remember wearing earlier. You look down at a crisp dress shirt[if player is wearing tie] and goldenrod tie[end if]. A fresh set of slacks sit comfortably on your hips, with a left leg that no longer bears the weight of an improvised splint, traded for a flex cast that is just barely visible under the slacks.
		
You remember your eye.
	
Tentatively, you put a finger up to your face, hoping you don't find the same empty socket. Your finger touches your left eyeball - still there. When you touch your right eyeball, you feel something cold.

You're suddenly self-conscious, and a little scared.".
	
Instead of examining yourself more than once during The Departure:
	if the player is in the Restroom:
		try examining the mirror;
	otherwise:
		say "You spruce up your cuffs a bit. You're still looking alright, you promise yourself. Nobody is looking at you or [if the mirror is examined]your eye[otherwise]whatever is in your eye socket[end if].".
	
Whitmore Executive is a region.

The Departure Lounge is a room in Whitmore Executive. "[if the gunfight is happening]The frenetic pace, the hurried slew of bodies, the order and linearity have all devolved at the sound of gunfire: the main departure lounge is now a huddle of frightened bodies, spilled coffee, and abandoned luggage. The concourse proceeds west and east, and Gate Six is to your south.[otherwise]People surround you; hurried, frayed, fast. Glass and steel beamwork cross in sweeping orbits around a large curved window that looks out over a greenbelted cityscape. The smell of hastily-fried foods mixed with heavy coffees. Black leather chairs embossed with friendly ORBGROUP logos line the main thoroughfare; white tile floors cut by vanishing-point aluminum slices extend on to the west and east until they curve out of sight. To your south is Gate Six.[end if]"

The suited man is a person in the Departure Lounge. "There is a suited man here, busy on his phone." The description of the suited man is "[if the gunfight is happening]The suited man has abandoned his phone and his luggage, and now looks frightfully around, silent, trying to breathe, with difficulty, unable to process the chaos in the terminal.[otherwise]The man looks like a thin splint of shale, dressed in a black and houndstooth violet suit. He wears a tapspike communicator on one ear, and you can see the readout of his mobile phone glowing against his face. He is dutifully tapping out a message.[end if]". Understand "hostage" as suited man.

Instead of talking to the suited man, say "[if the gunfight is happening]The man won't talk. He can't - he simply shivers, clearly afraid for his life.[otherwise]The man looks up from his mobile phone long enough to look you over and decide you can't possibly be talking to him. He goes back to his phone.[end if]".
Instead of asking suited man about something:
	say "[if the gunfight is happening]The man won't talk. He can't - he simply shivers, clearly afraid for his life.[otherwise]The man appears engrossed in his mobile phone. He lifts a finger of warning to you, indicating his task is more important than your question. You wait a moment, but he just keeps playing with his phone.[end if]".
	
[---seriously, the player will want to take the guy's phone---]

The window is a backdrop in the Departure Lounge. "You look out the window to a city towering over banks of uniform trees. Massive, spiny stacks mount over the green foreground, their muzzles snorting tall bursts of shimmering flame at hovering SeaSky platforms roping out in the distance. You can see a tower in the distance, taller than the other buildings, with a single filament extending upward into the sky."

The Departure Lounge East is a room in Whitmore Executive, east of the Departure Lounge. "[if the gunfight is happening]The east lounge is a haze of chalky white smoke that chokes out your breathing and burns your eyes. At the end of the concourse you can barely see the droplet sculpture, split in half around a ragged, burnt hole drilled violently through the acrylic. Travelers here are lying flat on their stomachs, hands over their ears, waiting for the shots to stop.[otherwise]The East lounge terminates in a spheroid droplet sculpture molded into the flesh of the building, casting a warped and eerie light from a bright, early morning onto the tile below your feet. The travelers that wait here seem anxious, strained for something, wearing unfocused expressions and slack ties.[end if] The rest of the lounge is back to your west. To your south is Gate Seven. A security monitor station is to the north.".

The droplet sculpture is a backdrop in Departure Lounge East. "[if the gunfight is happening]The proud sculpture now stands rudely torn in half, an explosion of some type having ripped a hole straight through the center at ground-level, leaving a jagged, charred entrance, through which pours an acrid gray haze.[otherwise]This sculpture is an example of a new and exciting form of architecture known as 'The New Reductionism', claims a plaque near the sculpture. The item itself - a five-foot high acrylic droplet - has a syrupy, poured-in texture that makes you wonder if a hidden strut is holding up the weight of the rest of the structure.[end if]".

The travelers is a backdrop in the Departure Lounge East. "[if the gunfight is happening]The travelers here are flat on their stomachs. They don't move - only rising and falling a little as they breathe, with their hands over their ears or interlocked behind their heads. They didn't just hear the crisis begin, they were close to it.[otherwise]Ties are out of place, shoes sit under chairs. Eyes stare anxiously at the board above your head, hoping for a change, an explanation, or something else.[end if] The gate just south of the seats is sealed, unmanned, and flagged with a red, blinking beacon.".

The Departure Board is a thing. It is in Departure Lounge East. It is fixed in place. "A departure board hangs from the ceiling. ". The description is "The departure board is a miniature Mercator projection of the world, crossed with shallow parabolas that indicate suborbital paths.

A red dot at the tip of Africa stands out. You gather this is your location at the moment.

Three indicators extend outward from the projection, in red polytext:
[fixed letter spacing][line break]MONDAY NOV 06 2141[line break]JO'BG - IK'SK 0X8 0900 GATE 5 - DELAYED[line break]JO'BG - AX'DA 2A2 0815 GATE 6 - [if agent is ready]BOARDING[otherwise]ENROUTE[end if][line break]JO'BG - BU'AR 5A1 0915 GATE 7 - DELAYED[line break]
[variable letter spacing]
"


Gate 7 is a room in Whitmore Executive, south of Departure Lounge East. Gate 7 is east of Gate 6. "[if the gunfight is happening]Gate Seven is full of travelers laying flat, trying not to be a part of any more gunfire. Smoke curls in from the departure lounge, coating the glass here with a gray film and making it hard to see.[otherwise]This gate, lit in wide, langorous strips by the refracted sunlight through the droplet carved into the East lounge, is filled with haggard business travelers, many of whom have resigned themselves to a long wait, as evidenced by rolled-up sleeves, loosened ties, and half-full cups of coffee stacked within emptied cups, a stack for each traveler. The ORBGROUP ticketing station is unmanned, and the gate's jetdoor indicator glows a solid, lazy red. [end if]The departure lounge is back to the north. Gate Six is to the west."

The laptop man is a person in Gate 7. "You see a man working on a laptop." 
The gate seven treasury agent is a person. "The treasury agent is pointing his weapon at you!" The printed name is "treasury agent".

Instead of shooting gate seven treasury agent with handgun:
	say "You raise your handgun and lay the sights over the agent.";
	say "He takes cover behind the seats!";
	say "[line break][bold type]*** BANG ***[roman type][line break]";
	say "The bullet buries in a nearby row of seats. You can't hit him from here!";


The Security Monitor is a room in Whitmore Executive, north of Departure Lounge East. "[if the gunfight is happening]The security monitor looks like a warzone. Scattered shell casings, plaster and tile fragments, shattered glass, and blood litter the floor. As you approach, you can see a foot around one corner, a slack face with open, lifeless eyes peeking from another.

In all, you count seven dead, all ORBGROUP security, leaving an empty security waystation - no conveyors move, no scanners operate. There is nothing moving but spreading blood and smoke.[otherwise]The tone of this part of the departure lounge is different from the rest. Here, the reality of international travel security encroaches on the convenient, on-the-go atmosphere of the lounge.

A waystation manned by ORBGROUP private security bristles with cameras, incoming and outgoing luggage belts, ion-alignment scanners, and rapier-like whip antennae. The whole operation is carried out behind an open rollgate, which you assume could close in case of an emergency.[end if]

To the north, you can see the rest of the airport, including the ticketing annex. To the south is the departure gate concourse."

The Security Gate is a door. It is north of the Security Monitor. It is open. It is unlocked. It is fixed in place. It is not openable.

The Ticketing Annex is a room in Whitmore Executive, north of Security Gate.

Instead of going north from the Security Monitor:
	if The Gunfight is happening:
		continue the action;
	otherwise:
		say "You approach the security station, but are stopped by a holographic 'NO' projected in front of you, punctuated by a disorienting series of red flashes from a laser distractor mounted above the security station.

Looks like you aren't supposed to go any further from here. Scowls from the security personnel behind the station indicate they aren't too happy with you trying to exit this way, either.";

The Departure Lounge West is a room in Whitmore Executive, west of the Departure Lounge. "[if the gunfight is happening]The recent gunfire has transformed the departure lounge into a river of panic - travelers are diving for cover, rapidly yelling to emergency services on mobile phones, or trying to find ways out of the terminal without exposing themselves to further harm. The scene is chaos - every traveler is out for themselves.[otherwise]The colors grow increasingly more saturated here in the west end of the departure lounge. The stark whites and metallic tones grow whimsical, with red paneling and green accents on the walls, flowing like tendrils out of the food court to the north. [end if]You can see the rest of the departure lounge concourse to the east, and Gate Five is to the south.".

Gate 5 is a room in Whitmore Executive, south of Departure Lounge West. Gate 5 is west of Gate 6. "[if the gunfight is happening]Gate Five is a sprawl of panicked bodies that take cover behind the ticketing terminal, lay on the floor, or attempt to unlock sealed doors to escape to the outer tarmac regardless of the danger of incoming ORBFLIGHTS. The jetdoor to the south is still sealed shut, though a group passengers is trying to open it.[otherwise]The west lounge terminates at Gate Five, a dark corner of the departure concourse, laid in with rich onyx on the westernmost wall. Few of the bands of light streaming in from the east concourse survive this far, and those that do paint a picture of silent, sullen travelers. Some are leaning against the wall in shadow, others lying on available chairs, trying to catch sleep under a deep red light from atop the sealed jetdoor. [end if]The departure concourse is to the north. To the east is Gate Six."

The bored man is a person in Gate 5. "You notice a man watching you." 
The gate five treasury agent is a person. "The treasury agent is pointing his weapon at you!" The printed name is "treasury agent".

Instead of shooting gate five treasury agent with handgun:
	say "You raise your handgun and shoot at the agent.";
	say "He drops down behind a row of terminals!";
	say "[line break][bold type]*** BANG ***[roman type][line break]";
	say "The bullet smashes into his cover. You can't hit him from here!";

The Food Court is a room in Whitmore Executive, north of Departure Lounge West. "The food court is a cascade of colors that emphasize poor judgment and the very latest horizontal combination services - surrounding you are a sea of coffeeweb shops, newsrental kiosks, burger-photo restaurants, astromedical prediction booths, and rapidbank-legal question-and-answer selfservice units. In the middle of the food court are a disorganized pile of seats and tables, peppered with tray and trash stations.

To the south, you can see the departure concourse. To the west, there's a hallway marked with a restroom sign."

The coffee-drinker man is a person in the Food Court. "You see a man drinking coffee." The description is "The man leans intently, almost greedily over a cup of coffee. His ashen coat drapes down to the floor, and his expression is quiet - your guess is that he has been here awhile.[if the gunfight is happening] It's a little strange - he seems to have moved to another part of the food court, though you're not sure why.[end if]".
Weaver is a person. Weaver is proper-named. "Weaver is chasing you!". The description is "[if activated]Weaver is a tall, sturdy man with a ruddy, wide face. He stands in an ashen trenchcoat framing a dark, waistcoated suit. The bulk of body armor is visible under his suit jacket, and his belt displays a silver federal badge.[otherwise]Weaver is curled up on the ground - you can see the places where bullets pierced his vest as he breathes in shallow bursts to stay alive. Blood stains his shirt, up to his collar, bending down in red cursive along his neck. His eyes are closed but his mouth, leaning close to his transmitter, is whispering something very faintly.

He is whispering - 'Help.'[end if]"

The Restroom is a room in Whitmore Executive, west of the Food Court. The description is "You look around the tiny restroom. It was made for the comfort of one person; the room is set out in dark marbles with Chinoise accent. The motion detector has already triggered a dim recessed panel of lighting, and you can see the basics of the room in fuzzy relief around you." 

Before going west from the food court, say "The restroom is a one-person affair. You enter and lock the door behind you."

Before going east from the Restroom, say "You unlock the door and head back out to the food court."

[the mirror]
The mirror is a thing in the Restroom. It is fixed in place. "You see your reflection in the mirror." The description is "You finally get a look at yourself in full, and the results aren't nearly as bad as you had hoped.

Your clothes are new, pressed, clean. No blood, dirt, rips - no evidence of what happened to you in The Bastille. Your hands are clean, though you notice evidence of cuts and punctures, puckered closed by ointments.

Your face - life without an eye isn't as debilitating as you had feared. There continues to be no evidence of recent foul play around the site of the missing eye. No bruises, blood, or infection. [if the player is wearing the eye]A prosthetic eye sits in the socket now, looking surprisingly like your remaining organic eye.[otherwise]With your prosthetic out, what remains is a pink, wrinkled patch of flesh under the lid - you can't help but close the lid to avoid the sight.[end if]

[if the player is wearing the eye and the player is carrying the boarding pass]You had better get this eye out before they close the gate on you."

A cleaning tool is a kind of thing.

The sink is a cleaning tool in the Restroom. It is fixed in place. The description is "The low silver basin sits in a bed of black marble flecked with gold. Looks functional enough."
The toilet is a cleaning tool in the Restroom. It is fixed in place. The description is "The fixture is made of a black porcelain. You could never figure that out - who actually thinks a black toilet is a good idea?"

The reflection is a backdrop in the Restroom.

Instead of examining the reflection when the player is in the Restroom, try examining the mirror.
Instead of examining yourself when the player is in the Restroom, try examining the mirror.

Gate 6 is a room in Whitmore Executive, south of Departure Lounge. The description is "[if the gunfight is happening]Gate Six is absolutely empty of life. No agent, no travelers - nothing. The panic has driven travelers to the far east and far west gates, leaving this middle gate barren.[otherwise]Gate Six is empty in comparison to the other two gates. Either a flight just departed from this gate or nobody is interested in the next flight. The window has a view of the tarmac, where you can see ground crews scurrying about, readying for an arrival or a departure. [end if]Gate Five is to the west, Gate Seven to the east, and the departure lounge is to the north. [if jetdoor is closed]The jetdoor blocks access to a jet bridge to the south.[otherwise]The jet bridge is open to the south.[end if]".
The ORBGROUP gate agent is a person in Gate 6. The ORBGROUP gate agent is either ready or unready. The ORBGROUP gate agent is unready. The description is "[if the gunfight is happening]The ORBGROUP gate agent is shaking underneath Clayton's grip, a blanched face containing eyes that dart rapidly from place to place, searching for a way out.[otherwise]The ORBGROUP gate agent stands behind a computer kiosk topped by a golden ORBGROUP logo. Clad in a blue regulation uniform, the agent looks uncomfortable, or at the very least intent on conveying that impression.[end if]"

Instead of talking to the ORBGROUP gate agent:
	If the ORBGROUP gate agent is unready:
		say "The agent appears busy at a computer console. The agent refuses to look up, instead waving you away. 'This orbflight hasn't arrived yet, Sir. Please wait in the terminal.'";
	otherwise:
		If the ORBGROUP gate agent is not carrying the ticket:
			say "The agent looks at you, trying to gauge something.
			
			'Are you our last passenger? We've been calling you. Ticket, please.'";
		otherwise:
			say "The agent looks at you with concern.
			
			'Sir,' the agent says, 'please proceed to the Restrooms, remove your prosthetic, and come back here. We need you on board - minus your prosthetic - before we disconnect from the gate.'".
		
Instead of asking the ORBGROUP gate agent about something, try talking to the ORBGROUP gate agent.

Instead of giving the ticket to the ORBGROUP gate agent:
	if the agent is unready:
		try talking to the agent;
	otherwise:
		now the ORBGROUP gate agent is carrying the ticket;
		now the player is carrying the boarding pass;
		say "You hand over your ticket to the agent, who takes it as you pass through a bioscanner. You see a red light go on above your head and a hand stops you from going any further, resting firmly against your chest.[line break]
	
	'Sir,' the agent says at last, with a measured, slow voice, removing his hand, 'Your right eye is a prosthetic, isn't it?'[line break]
	
	'I - ' you manage, your hand edging up toward your face in embarassed reflex.[line break]
	
	'We're required to tell you, Sir,' the agent continues, 'that prosthetic eyes are not allowed on any ORBGROUP flight. They have the habit of becoming projectiles when the entry vehicle decelerates.'[line break]
	
	You hesitate, unsure what to say next.[line break]
	
	The agent has already calmly printed out a boarding pass and handed it to you. 'ORBGROUP policy - if you would head to the restrooms and remove the prosthetic before flight. Sir.'[line break]
	
	'You want me to take my own eye out?' you ask.[line break]
	
	'The restrooms are in the food court, just north of Departure Lounge West, Sir. And if you could hurry, Sir. We're fueled and ready to clear the gate.' The agent returns to the scanner, leaving you with the pass.[line break]
	".
The Jetbridge is a room in Whitmore Executive. It is south of the jetdoor. The jetdoor is a door. The jetdoor is locked. The jetdoor is south of Gate 6. The description is "[if closed]The jetdoor is a heavy-looking spinelock affair, with a sterile-white depressurization rig attached to the side. It is closed, a blood-red light pulsing slowly above the door frame.[otherwise]The jetdoor lies wide open, bathed in a green light, signaling equalized pressure and access between Gate 6 and the jetbridge.[end if]"

The pass reader is a container. It is in Gate 6. It is fixed in place. The description is "The pass reader has a blinking green light indicating it is ready to accept a boarding pass for this flight."

Instead of inserting anything (called the inserted) into the reader:
	if the inserted is the boarding pass:
		if the player is wearing the eye:
			try talking to the agent;	
		otherwise:
			now the jetdoor is unlocked;
			now the jetdoor is open;
			remove the boarding pass from play;
			say "The reader sucks down the pass, and the jetdoor in front of you unseals with a spout of gas from the depressurization rig. Now open, the jetdoor access light is green. You're cleared to enter the jetbridge to your south.";
	otherwise:
		say "The reader doesn't look like it'll accept that.".

[the boarding pass]
The boarding pass is a thing. The description is "The pass is stamped 'TEMPORARY - MEDICAL' - and has the flight number to Alexandria on it."

[the flex cast]
The flex cast is a thing. The description is "The cast is tight on your leg, but not too uncomfortable. You can see the cast move delicately against you as microcapsules inside inflate and deflate under computer control to match pace with your movements.

Someone wanted you to heal right."

Instead of taking off the flex cast, say "You had better not mess with the cast - looks like it was put on professionally."

[the tie]
The tie is a thing. It is wearable. The description is "The tie is a silk Madrassas in goldenrod. It looks new, and ironed."

[the prosthetic eye]
The prosthetic eye is a thing. It is wearable. The description is "[if player is wearing eye and player is not in the restroom]You can't see it, since it is in your eye socket, but it feels like something made of a glass-like material.[otherwise]The eye is made out of a kind of acrylic. It isn't a sphere - it's a hemispherical hull. You aren't sure, but you suspect that means the eye must sit on top of something that was left in your eye socket. That thought makes you uneasy.[end if]"

Instead of taking the eye when the player is wearing the eye:
	try taking off the eye.
	
Instead of taking off the eye:
	If the agent is not carrying the ticket:
		say "You're not sure you want to touch that right now.";
	otherwise:
		if the player is not in the restroom and the player is not in Gate 6:
			say "You don't want to touch it here, people are looking - and you haven't even washed your hands!";
		if the player is in Gate 6:
			say "The agent stops you. 'Not here, Sir. Have some respect for the other passengers.' The restrooms are in the food court, just north of Departure Lounge West.";
		if the player is in the Restroom:
			say "You aren't really sure how to do this - you always hated anything getting close to your eyes...but you're running out of time to catch your flight....
			
			
			You try to pinch the eye with one hand while keeping your lid open with the other - 
			
			
			Whoops! The eye falls into your hand. It's a hull, not a sphere like you imagined. It's hard like glass, and cold.
			
			Wait a minute. On the back there's some kind of tape or film.
			
			You carefully peel it off.
			
			";
			now the player is carrying the eye;
			now the player is carrying the film;
			remove the ORBGROUP gate agent from play;
			
Instead of wearing the eye when the player is not wearing the eye:
	say "Not right now. Taking it out was hard enough."

understand "take out [something wearable]" as taking off.

[the film]
A thing can be washable or unwashable. A thing is usually unwashable.
A thing can be dirty or clean. A thing is usually clean.

The thin film is a thing. It is dirty. It is washable. The description is "[if dirty]The film is a very thin, rectangular strip of something flexible. It looks like there's something written on it, but it's covered by some kind of cloudy coating. There must be some way to get it off.[otherwise]On the film is written the following words:[line break][line break][fixed letter spacing]DO NOT GET ON THE PLANE[variable letter spacing][end if]"


[washing the film]
Cleaning it with is an action applying to two things.
Understand "wash [something] in [something]" as cleaning it with.
Understand "wash [something] with [something]" as cleaning it with.
Understand "clean [something] in [something]" as cleaning it with.
Understand "clean [something] with [something]" as cleaning it with.
Understand "rinse [something] in [something]" as cleaning it with.
Understand "rinse [something] with [something]" as cleaning it with.

Instead of rubbing something washable:
	say "You can't seem to get [the noun] clean on its own.".


Carry out cleaning it with:
	if the noun is not washable:
		say "You really don't see a need to wash [the noun].";
	otherwise if the second noun is not a cleaning tool:
		say "You're not sure how [the second noun] is going to get [the noun] any cleaner.";
	otherwise:
		say "You scrub at [the noun] a bit.";
		now the noun is clean.

Before cleaning film with sink, say "You turn the spigot on the sink and let the water run over the film."
Before cleaning film with toilet, say "Yuck. Water is water, though. You dip the film in the bowl."
After cleaning film with a cleaning tool, say "You pull the film back and take a look at it. Looks clean - there's something written on it."




[the gunfight scene]

The Gunfight is a scene. The Gunfight begins when the Player is carrying the prosthetic eye.

The Gunfight ends On The Plane when the player is in the ORBFLIGHT Cabin.
The Gunfight ends With Clayton when the player is in the Ticketing Annex.
The Gunfight ends With Treasury when surrendering.

When The Gunfight begins:
	now the handgun is in the Departure Lounge;
	now the ORBGROUP agent is in the Departure Lounge;
	now the ORBGROUP agent is undescribed;
	now Clayton is in the Departure Lounge;
	now Clayton is activated;
	now all rooms are unvisited;
	now the suited man is activated;
	now the Food Court is unvisited;
	now the description of the Food Court is "Things seem quiet in the food court now - maybe everyone has boarded for Gate Six.";
	now the initial appearance of coffee-drinker man is "The man drinking coffee has moved to another table.".
	
When The Gunfight ends:
	change the command prompt to ">".

A door can be activating or inert. Doors are usually inert.

A firearm is a kind of thing. A firearm has a number called round count. Firearms usually have round count 0.

Definition: something is ungunlike if it is not a firearm.

Shooting it with is an action applying to two things.
Understand "shoot [something] with [something]" as shooting it with.
Understand "shoot [something] at [something]" as shooting it with (with nouns reversed).
Understand "fire [something] at [something]" as shooting it with (with nouns reversed).
Understand "shoot [something ungunlike]" as shooting it with.
Understand "shoot at [something ungunlike]" as shooting it with.
Understand "fire at [something ungunlike]" as shooting it with.
Rule for supplying a missing second noun when shooting something with: 
    change the second noun to the handgun.

Check shooting it with:
	if the second noun is not a firearm:
		say "You can't shoot [the noun] with [the second noun] - it's not a firearm!" instead;
	otherwise:
		if the player is not carrying the second noun:
			say "But you're not carrying [the second noun]!" instead;
		if the round count of the second noun is 0:
			say "You pull the trigger and the gun clicks impotently." instead.

Carry out shooting it with:
	say "You do your best to get [the noun] in the sights of [the second noun]...you pull the trigger.[line break][bold type]*** BANG ***[roman type][line break]";
	say "The bullet careens off into space - you missed.";
	

Before shooting anyone with the handgun during The Gunfight:
	if the player is not carrying the handgun, say "But you're not carrying anything to shoot [the noun] with." instead.
	
Before going south from the food court during The Gunfight:
	now Weaver is in the food court;
	now Weaver is activated;
	remove the coffee-drinker man from play;
	say "You hear a click from behind you.";
	say "'Don't move', a voice says from the food court. 'United States Treasury Department.'";
	say "You freeze up - not sure if you should run or stay.";
	say "'That's good', the voice says behind you, finally coming around to face you. It is the man from the food court, now brandishing a compact pistol, and wearing a badge and earpiece. 'We know you, but you don't know me. I'm Joseph Weaver, Financial Crimes Division.' Weaver looks you in the eyes, but you find it hard not to stare at the little black pistol he points right at you.[line break][line break]";
	say "Before he can say any more, an alarm sounds throughout the concourse.[line break][line break]";
	say "'What the hell -' Weaver starts, but is interrupted by gunshots down the concourse to the east - followed closely behind by screams as travelers in the departure lounge panic at the shots.[line break][line break]";
	say "You break from him and run south to the departure lounge!";
	now the Departure Lounge West is unvisited;
	now Clayton is in the Departure Lounge;
	now the initial appearance of Weaver is "Weaver is chasing you!";
	change the command prompt to "(type SURRENDER to give up) >";

After going south from the food court during The Gunfight:
	Weaver catches up with you in one turn from now;
	say "You can see Weaver catching up with you! You had better get out of here.";
	try looking.

Instead of going north from Departure Lounge West during The Gunfight:
	say "You look to the north to try to find a way back to the food court, but Weaver anticipates your action, cutting you off with a shot that nearly clips the side of your head, forcing you down to the floor!";
	say "North is no good. You've got to find another way!".
	
Before going south from Departure Lounge West during The Gunfight:
	say "You decide to go South, diving into cover behind a planter!";
	now Gate 5 is unvisited;
	now the gate five treasury agent is in gate 5;
	now the gate five treasury agent is activated;
	remove the bored man from play;

After going south from Departure Lounge West during the Gunfight:
	Weaver catches up with you in two turns from now;
	say "The man against the wall suddenly turns toward you, a gun and badge coming into view! 'Treasury Department! Get down on the ground!'";
	try looking.
	
Instead of going north from Gate 5 during The Gunfight:
	say "You try to find a way north out of the gate, back to the concourse. Peeking up above the planter, you see Weaver already maneuvering toward you. Before you can take a step, he blasts a chunk out of the planter just inches from your head. As you spit out soil and clay dust, you realize you're not going to go north from here.";
		
Before going east from Gate 5 during The Gunfight:
	say "You decide to dart for Gate Six, to the east. Picking your moment, you dive to the next row of chairs, hearing bullets cross overhead.".
	
After going east from Gate 5 during The Gunfight:
	if Clayton is in the Departure Lounge and Clayton is activated:
		say "There's somebody north of you - he's calling for you! 'Come to me! I'm not with the Treasury! Don't get on that plane!'";
	say "To the south, the jetdoor is closed, but the reader is blinking."; 
	Weaver catches up with you in two turns from now;
	try looking.
	
Before going south from Gate 6 during The Gunfight:
	if the jetdoor is open:
		say "You make a break for the open jetdoor!";
		if Clayton is in the Departure Lounge and Clayton is activated:
			say "You hear the man north of you, yelling! 'Come back, damn you! Don't get on that -'";
			say "His words are drowned out in a burst of automatic gunfire.";
			the SWAT team takes Clayton down in zero turns from now;
			say "You keep your head down and run...";
	otherwise:
		say "You make a break for the jetbridge...".

After going south from Gate 6 during The Gunfight:
	say "You make it into the jetbridge, breathless - but Weaver is still coming for you. You look around you...";
	Weaver catches up with you in five turns from now;
	try looking.

Before going east from Gate 6 during The Gunfight:
	say "You decide to try for Gate Seven! You pick a point halfway down the row of seats at the east end of the gates, and go!";
	the SWAT team enters in zero turns from now.

After going east from Gate 6 during The Gunfight:
	say "The man with the laptop at Gate Seven shields himself over a row of seats, training a gun on you! 'US Treasury! Drop to your knees and put your hands behind your head!'";
	now the gate seven treasury agent is in gate 7;
	now the gate seven treasury agent is activated;
	remove the laptop man from play;
	Weaver catches up with you in two turns from now;
	try looking.
	
Instead of going west from Gate 7 during The Gunfight:
	say "Before you can run west back to Gate Six, the agent at Gate Seven shoots at you, nearly catching you in the back. You drop down to the floor to avoid another shot and realize you'd never make it - there's simply too much ground to cover.".
	
Instead of going north from Gate 7 during The Gunfight:
	say "The east concourse is a mess of smoke and noise, but you decide it is better than here. As you break for the concourse, shots explode all around you, some from the smoke, some behind you, some to the west, and you realize you're trapped! You drop to the ground and crawl back to Gate Seven, scraping your arms on broken glass.".
	
Before going north from the jetbridge during The Gunfight:
	if Weaver is in the jetbridge and Weaver is activated:
		say "Weaver stops you, putting his gun in your face. 'You're trapped! Surrender!'" instead;
	otherwise if the SWAT team is in the jetbridge:
		say "The SWAT team leader moves up on you before you can get out, and slams you back into the jetbridge. 'Drop it now!' He shouts, pointing his gun in your face.";
	otherwise:
		continue the action.
		
[END THREE - CLAYTON - ESCAPES WITH TRENT CLAYTON]

Hostage-taking relates one person to one person.

The verb to capture (he captures, he captured, he is captured) implies the hostage-taking relation.

The handgun is a firearm. It has round count 15. "You see a handgun on the ground."

Clayton is a person. Clayton is proper-named. "Clayton is here, holding the ORBGROUP agent hostage.".

The ORBGROUP agent is a person. The ORBGROUP agent is captured by Clayton.


Before going east from Departure Lounge West during The Gunfight:
	say "You bolt down the departure concourse, headed east!";
	
Instead of going west from Departure Lounge during The Gunfight:
	if Weaver is activated:
		say "You try to head back west to Gate Five and the West Concourse, but Weaver is right on your tail, and he cuts off your escape, sending you collapsing to your hands and knees behind a kiosk as his shots fly overhead. There's no way you're getting there.";
	otherwise:
		say "As you break for the west concourse again, a single shot scrapes by you, nearly taking off your ear - you see [if gate 5 is unvisited]a man[otherwise]the treasury agent[end if] at Gate Five shooting across the concourse at you, preparing to take another shot! You won't make it there.";
	
After going to the Departure Lounge when the Departure Lounge is unvisited during The Gunfight:
	say "'Over here!' shouts a voice from down the concourse. 'Did you get my note?'";
	say "'Where are you?' you shout, taking cover behind a planter.";
	say "'Right here', the voice says, the speaker appearing behind a very frightened-looking ORBGROUP gate agent. You notice the agent has a gun to their head, and the mysterious stranger is holding it, keeping the agent in a body-shield grip in front of him.";
	say "'What the hell -' you start to ask, but duck back down as Weaver approaches.";
	say "'Trying to survive,' the man says. 'Now it's your turn. Grab that man in the suit in the corner and let's go. Catch.' He tosses something in the air - it slides on the tile toward you, stopping at your feet. It is a nickel-plated handgun.";
	say "'My name is Clayton, by the way. Let's go.'";
	Weaver catches up with you in three turns from now;
	the SWAT team enters in one turn from now;
	now the initial appearance of the suited man is "The suited man huddles for cover nearby.";
	try looking.
	
Understand "capture [something]" as taking.
Understand "grab [something]" as taking.
Understand "take [something] hostage" as taking.

Before going east from the Departure Lounge when the Departure Lounge East is unvisited during The Gunfight:
	if the suited man is not captured by the player and Clayton is in the location and Clayton is activated:
		say "'Stop! Grab that hostage!	That SWAT team will take you down in a heartbeat if you don't!' Clayton yells." instead.

After going to the Departure Lounge East when the Departure Lounge East is unvisited during The Gunfight:
	if Clayton is in Departure Lounge East:
		say "You follow Clayton to the east departure lounge.";
		Clayton makes his move north in three turns from now;
	otherwise:
		say "You make a break for the east departure lounge.";
		Clayton makes his move east in zero turns from now;
		Clayton makes his move north in two turns from now;
	Weaver catches up with you in three turns from now;
	say "You are confronted by a SWAT team staring you down from the east corner of the building where they have breached a ragged hole through the sculpture.";
	try looking.
	

After going to the Security Monitor when the Security Monitor is unvisited during The Gunfight:
	if Clayton is in Security Monitor and Clayton is activated:
		say "You follow Clayton north to the security monitor.";
	otherwise if Clayton is activated:
		say "You make a break for the security monitor.";
		say "'Goddamnit,' Clayton yells, dragging his hostage to catch up with you.";
		Clayton makes his move north in zero turns from now;
	Clayton goes for the gate in three turns from now;
	the SWAT team makes their move north in one turn from now;
	Weaver catches up with you in two turns from now;
	say "As you approach the security monitor, you realize a gunfight has already happened here, and there are more than a half-dozen dead - all security personnel.";
	try looking.
	
Before of going north from the Security Monitor during The Gunfight:
	if the Security Gate is closed:
		continue the action;
	otherwise if the Security Gate is inert:
		say "You realize you could bolt for the gate - but it's too far a distance! The SWAT team would cut you down before you got through. What to do?" instead;
	otherwise:
		say "You realize this is your chance to get out. Clayton has started the gate closing, and you still have time to get through.";
		say "You shove the hostage at the SWAT team, holding them up for just the moment you need to break for the gate!";
		say "Shots ring out around you, and you dive for the rapidly-shrinking opening in the gate in time to get through!";
		continue the action;
		

After going north from the Security Monitor during The Gunfight:
	say "As you emerge on the other side of the gate, the bulletproof metal and glass absorbing a hail of bullets, a hand reaches down for you.";
	say "'How's it going there, ace?' Clayton asks, smiling, as he pulls you up. 'We made it.'";
	now the player is in the Johannesburg ORB Tarmac.
		
	
	
Instead of going south from the Security Monitor during The Gunfight:
	say "You try to head back to the concourse, but the SWAT team fires over your head, the bullets snapping overhead and dropping you and your hostage to the ground. You manage to keep a hold on him - but it's clear that you can't go back now.";

Before going west from Departure Lounge East during The Gunfight:
	if the SWAT Team is in Departure Lounge East:
		if Clayton is in Departure Lounge East and Clayton is activated:
			say "Clayton watches you run west. 'Goddamnit it -'[line break]";
			say "His words are drowned out by a hail of gunfire.";
			now the initial appearance of Clayton is "Clayton lies on the ground, bleeding out.";
			now Clayton is deactivated;
			if the ORBGROUP agent is captured by Clayton:	
				say "The ORBGROUP agent Clayton was holding hostage runs away screaming, suddenly free.";
				now Clayton does not capture anybody;
			continue the action;
		otherwise:
			now the initial appearance of the SWAT team is "The SWAT team is preparing to shoot! Do something!";
			the SWAT team kills the player in two turns from now;
			say "The SWAT team leader yells out! 'Suspect! Do not try to run! You are surrounded, and we will take you down unless you surrender immediately!'" instead;
			
Before going south from Departure Lounge East during The Gunfight:
	if Clayton is activated:
		say "'Hey!' Clayton yells as you run south. 'Hey, get back here, goddamn-'";
		say "A hail of gunfire drowns his words out.";
		Weaver catches up with you in three turns from now;
		the SWAT team takes Clayton down in two turns from now.


After taking the handgun for the first time during The Gunfight:
	say "You reach down and grab the handgun. It is heavy and cold.";
	say "'Good! Grab that man - we're getting out of here!' Clayton yells.";
	say "The suited man looks at you with pleading eyes. 'Don't', he mouths silently to you.";
	change the command prompt to "(SURRENDER to give up, SHOOT to shoot someone) >";
	
Instead of shooting Clayton with handgun the first time:
	say "You put Clayton in your sights and squeeze the trigger.";
	say "[line break][bold type]*** BANG ***[roman type][line break]";
	say "The bullet goes astray, carrying off a piece of a nearby column.";
	say "'What the fuck -' Clayton yells, ducking his head.".
	
Instead of shooting Clayton with handgun the second time:
	say "You put Clayton in your sights and squeeze the trigger.";
	say "[line break][bold type]*** BANG ***[roman type][line break]";
	say "The bullet catches Clayton in the shoulder, dropping him to the floor.";
	now the initial appearance of Clayton is "Clayton lies on the ground, bleeding out.";
	now Clayton is deactivated;
	if the ORBGROUP agent is captured by Clayton:	
		say "The ORBGROUP agent Clayton was holding hostage runs away screaming, suddenly free.";
	now Clayton does not capture anybody;
	
Instead of shooting Clayton with handgun when Clayton is deactivated:
	say "You can't bring yourself to shoot him while he's on the ground.".
	
Instead of shooting suited man with handgun during The Gunfight:
	if the suited man is deactivated:
		say "You can't bring yourself to shoot him while he's on the ground.";
	otherwise if the suited man is captured by the player:
		say "You put the gun up close to his head and pull the trigger.";
		say "[line break][bold type]*** BANG ***[roman type][line break]";
		say "You feel him go limp in your arms. You let go, and he falls to the floor in a heap.";
	otherwise:
		say "You put the man in your sights and squeeze the trigger.";
		say "[line break][bold type][line break][bold type]*** BANG ***[roman type][line break][roman type][line break]";
		say "You see the man collapse in a pool of blood on the ground.";
	now the initial appearance of suited man is "The suited man is bleeding to death on the ground.";
	now the description of the suited man is "The man lies in slack collapse, a faintly-breathing lump of charcoal houndstooth suit, blood leaking from his gunshot wounds.";
	now suited man is deactivated;
	now suited man is not captured by anybody;
	now player captures nobody;
	if Clayton is in the location and Clayton is activated:
		say "Clayton looks at you, horrified. At last, he appears able to speak. 'That - is fucking - wonderful. You're on your own, you fuck-up.'";
		if Clayton is in the Departure Lounge:
			Clayton makes his move east in zero turns from now;
			the SWAT team takes Clayton down in two turns from now;
	
Instead of taking suited man when the player is in the Departure Lounge during The Gunfight:
	if the suited man is deactivated:
		say "He is lying in a pool of his own blood. He's gone - you won't be able to take him hostage.";
	otherwise if the player is carrying handgun:
		say "You cock the hammer back on your handgun and grab the man by his collar. Shrieking, he backpedals right into you. You feel his back slam against your chest, and your arm crooks around him to keep him close.
		
		
		You swallow and close your eyes, putting your gun to his head, trying to convince yourself you're only doing this to survive.
		
		
		Just to survive.";
		now the player captures the suited man;
		now the suited man is activated;
		now the initial appearance of the suited man is "The suited man is your hostage, breathing raggedly under your arm.";
		Clayton makes his move east in zero turns from now;
	otherwise:
		say "You try to grab the man, but he launches at you with a flailing arm that catches you across the jaw and sends you backward.";
		say "He doesn't want to go.".
		
Every turn during The Gunfight:
	if the player captures the suited man and the suited man is not in the location and the suited man is activated:
		now the suited man is in the location;
		say "You carry the suited man with you. He struggles under your grip, trying to twist away from the gun you hold to his head.";
	if the ORBGROUP agent is captured by Clayton and the location of the ORBGROUP agent is not the location of Clayton:
		now the ORBGROUP agent is in the location of Clayton;
	if the SWAT team is in the location of the player:
		if the player captures the suited man:
			say "'Drop your hostage and your weapon!' the SWAT team leader commands. 'Surrender now!'";
			if Clayton is in the location of the player and Clayton is activated and Clayton captures the ORBGROUP agent:
				say "'Don't listen', Clayton says. 'We have the hostages, we have the upper hand. Just follow me.'";
			if Weaver is deactivated:
				say "'Get trailers in here now, Weaver needs an EMT!' the SWAT team leader shouts. 'Take that suspect down!'";
				say "'Can't get a shot, sir!', the marksman yells back.";
		otherwise:
			say "'Target has no collateral', the SWAT team leader yells. You realize you have no hostage - nothing to protect you - and a gun in your hand! 'Take him down!'";
			the SWAT team kills the player in zero turns from now;
	if Weaver is in the location of the player and Weaver is activated:
		if the player captures the suited man:
			say "Weaver keeps his focus on you, his eyes not straying from yours - he is watching you, judging you.";
			if a random chance of 1 in 5 succeeds:
				say "'Just let the man go,' Weaver says, calmly. 'Just surrender - we'll all live through this.'";
		otherwise:
			say "Weaver steadies his gun on you. 'You're not getting out of here like this.'".
		
			
Instead of dropping handgun during The Gunfight:
	say "No - now that you have it, it's your only ticket out. You're going to keep it.".

Before going south from the Departure Lounge during The Gunfight:
	if Clayton is activated:
		say "'Hey!' Clayton yells as you run south. 'Hey, get back here, goddamn-'";
		say "A hail of gunfire drowns his words out.";
		Weaver catches up with you in three turns from now;
		the SWAT team takes Clayton down in two turns from now.

Instead of shooting Weaver with handgun:
	if Weaver is not in the location:
		say "You can't hit him from here!";
	otherwise if Weaver is activated:
		if the player is in the jetbridge:
			say "You raise your gun to fire.";
			say "Weaver rushes forward, trying to disarm you.";
			say "[line break][bold type]*** BANG ***[roman type][line break]";
			say "The gun goes off - Weaver drops to the ground, bleeding out.";
			say "You quickly close the jetdoor and yank the pressurization handle.";
			now Weaver is deactivated;
			now the initial appearance of Weaver is "Weaver lies on the ground, not moving.";
			now the panel is open;
			now the jetdoor is closed;
			try pulling the handle;
			the ORBFLIGHT Cabin opens in zero turns from now;
			the SWAT team tries opening the jetdoor in one turn from now;
		otherwise if the player is in Departure Lounge East:
			say "You snap your gun up and fire a shot at Weaver.";
			say "[line break][bold type]*** BANG ***[roman type][line break]";
			say "Weaver collapses to the ground.";
			say "Suddenly, you hear the SWAT team leader yell to his men 'Target has gone hot!'";
			the SWAT team kills the player in zero turns from now;
		otherwise if the player is in Security Monitor:
			say "You raise your handgun and lay the sights over Weaver.";
			say "He raises his gun in response.";
			say "[line break][bold type]*** BANG ***[roman type][line break]";
			say "The gun goes off - Weaver crumples to the ground.";
			now Weaver is deactivated;
			now the initial appearance of Weaver is "Weaver lies on the ground, not moving.";
		otherwise:
			say "You raise your handgun and lay the sights over Weaver.";
			say "He takes cover!";
			say "[line break][bold type]*** BANG ***[roman type][line break]";
			say "The bullet tears a chunk out of the tile near his head, missing him.";
	otherwise:
		say "You point your gun at Weaver as he lay on the ground.";
		say "[line break][bold type]*** BANG ***[roman type][line break]";
		say "His body twitches as you put another bullet in him.";
		
Instead of shooting the SWAT team with handgun:
	say "You realize you're outnumbered, but you don't care. You raise the gun and fire a round at the SWAT team leader!";
	say "[line break][bold type]*** BANG ***[roman type][line break]";
	say "The bullet catches him in his face shield, sending him careening to the ground.";
	the SWAT team kills the player in zero turns from now.

At the time when the SWAT team kills the player:
	if the SWAT team is in the location of the player:
		say "You hear a series of loud bangs as the SWAT team fires on you. The wind rushes out of your lungs as you crumple to the ground.";
		end the game saying "You have been shot to death.";

The SWAT team are people. "A SWAT team has their guns sighted right on you!".

		
[END ONE - JOHN FRENCH - MAKES IT ON AIRCRAFT]

The pressurization panel is a container in the jetbridge. It is fixed in place. It is closed. It is openable.
The emergency pressure handle is a thing in the pressurization panel. The handle is fixed in place. The handle can be pulled.

Before pulling handle when the player is in the jetbridge:
	say "You yank at the handle...".
	
Instead of pulling handle when the player is in the jetbridge:
	if Weaver is in the jetbridge and Weaver is activated:
		say "But Weaver stops you, bringing his gun down on your hand.";
	otherwise if the SWAT team are in the jetbridge:
		say "The SWAT team fires at you, causing you to jerk away from the panel and slam against the wall!";
	otherwise if the jetdoor is open:
		say "The handle won't pull out! An orange warning light blinks next to the handle - something's not right.";
	otherwise:
		if the player captures the suited man:
			say "Your hostage, realizing he is about to be trapped, slips away from you, out of the jetbridge.";
			now the player captures nobody;
			remove the suited man from play;
		say "The whole thing comes off in your hand; you slam back against the wall as a yellow light flares from above and a blast of escaping gas rushes down the jetbridge corridor.
		
		The light turns red - you see a wheel mechanism inside the jetdoor turn and lock into place.
		
		You have sealed the jetdoor closed. Gunshots ring out from the airport side of the door, but they are barely audible.";
		now the jetdoor is locked;
		remove the handle from play;
		change the command prompt to ">";
		

[END TWO - SURRENDER - SURRENDER TO TREASURY]

Surrendering is an action applying to nothing.

Understand "surrender" as surrendering.

Carry out surrendering:
	if The Gunfight is happening:
		say "You realize there's no way out of here alive if you don't give up.";
		if the player is carrying the handgun:
			say "You drop your weapon to the ground. It clatters and is still.";
		if the player captures the suited man:
			say "You release your hostage - feeling him jerk away at the newly-found freedom.";
		say "You get down on your knees, putting your hands behind your head.";
		if Clayton is in the location and Clayton is activated and the SWAT team is in the location:
			say "Clayton sees you drop your weapon and surrender. 'Oh, goddamn it -'[line break]";
			say "His words are drowned out by a hail of gunfire.";
			say "You look away, only seeing a flicker of his body falling to the ground.";
		now the player is in the NEHI Cell;
	otherwise:
		say "That's not a verb I recognise.".
		

[swat team entrance]
At the time when the SWAT team enters:
	if the SWAT team is deactivated:
		say "A crash from the east concourse knocks you to the floor - smoke belches in from the east, and flickering flashlights are cutting bright planes in the oncoming cloud. You count five of them, and they are advancing fast.[line break][line break]";
		now the SWAT team are in the Departure Lounge East;
		now the SWAT team are activated;
		if Clayton is in the location and Clayton is activated:
			say "'Shit,' Clayton says. 'I was afraid of that. Treasury sent a SWAT team.' You can see him tighten the grip on his hostage. 'You get your hostage and you get ready to move! We do this together or we're both dead.'";
		Clayton makes his move east in three turns from now;

At the time when the SWAT team takes Clayton down:
	if Clayton is in the location:
		say "You see Clayton, stripped of his hostage, gun drawn, facing down a rapidly-advancing SWAT team. With a rake of automatic fire, Clayton is cut down to the floor, collapsing into a bloody heap.
		
		With a uniform movement, the SWAT team now turns its guns on you.";
	otherwise:	
		say "There is a rapid, ripping sound of gunfire from the east concourse, followed by ominous quiet. Calls for a medic and shouts of cleared sectors eventually fill in the silence. The SWAT team must have taken [if departure lounge is unvisited]the man in the departure lounge[otherwise]Clayton down[end if].
	
	You know that they're coming for you next.";
	now the SWAT team are in the location of Clayton;
	the SWAT team goes for the player in two turns from now;
	now Clayton is deactivated;
	remove Clayton from play;
	remove ORBGROUP agent from play.
	
At the time when the SWAT team goes for the player:
	if the player is in the jetbridge and the jetdoor is closed and the jetdoor is locked:
		the SWAT team tries opening the jetdoor in zero turns from now;
	otherwise:
		now the SWAT team are in the location of the player;
		say "The SWAT team has closed in on you! They are approaching, ready to shoot!";
		now the initial appearance of the SWAT team is "The SWAT team is preparing to shoot! Do something!";
		the SWAT team kills the player in two turns from now;
		the SWAT team goes for the player in three turns from now;
	
At the time when Clayton makes his move east:
	if Clayton is activated:
		if the player is in the Departure Lounge and Clayton is in the Departure lounge:
			if the player captures the suited man:
				say "'Okay,' Clayton says, turning his hostage to face the incoming SWAT team. He cranes his head over your way. 'We're making our move now! Let's go!'";
			otherwise if the suited man is activated:
				say "'Hurry this shit up! Get that guy and follow me!' Clayton yells, hooking his arm tight around his hostage.";
		say "Clayton takes his hostage east.";
		now Clayton is in Departure Lounge East;
	
At the time when Clayton makes his move north:
	if Clayton is activated:
		if the player is in the Departure Lounge East and Clayton is in Departure Lounge East:
			say "Clayton looks over the shoulder of his hostage, fixing his eyes on you. 'Ready? Head north with me, now!'";
			say "Clayton takes his hostage north.";
		now Clayton is in the Security Monitor;
		
At the time when the SWAT team makes their move north:
	now the SWAT team is in the security monitor;
	if the player is in the security monitor:
		say "The SWAT team moves north, following you to the security monitor, their guns still trained on you!";
		say "'Suspect! You have no way out. You can't run - you can't fight. Surrender now!";
	if Clayton is activated:
		say "'Go fuck yourselves,' Clayton yells at the SWAT team. He turns toward you. 'Don't worry - I'll think of something. Just give me a second.'";
	if Clayton is deactivated:
		now the initial appearance of the SWAT team is "The SWAT team is preparing to shoot! Do something!";
		the SWAT team kills the player in two turns from now;

		
At the time when Clayton goes for the gate:
	if Clayton is activated:
		say "'We're getting out of here,' Clayton says. 'You've got to trust me. Keep that hostage close and cover me!'";
		say "Suddenly, Clayton ducks away into the security monitor booth, past where you can see him!";
		remove Clayton from play;
		remove ORBGROUP agent from play;
		the gate starts closing in two turns from now.
	
At the time when the gate starts closing:
	say "You hear a loud whine, and a rattle - you realize the security gate north of you is closing!";
	say "'Hey!' you hear Clayton yell from the security monitor. 'Come on! Come north before the gate closes!'";
	now the security gate is activating;
	the gate is shut in two turns from now.
	
At the time when the gate is shut:
	say "The gate shuts with a final slam of metal on tile. The magnetic locks click into place.";
	say "You're alone now. Clayton is gone.";
	say "The SWAT team leader orders his men to close in on you. 'Suspect! This is it - nowhere to run. Surrender now, or we will take you down.'";
	now the initial appearance of the SWAT team is "The SWAT team is preparing to shoot! Do something!";
	now the Security Gate is closed;
	now the Security Gate is locked;
	now the Security Gate is inert;
	the SWAT team kills the player in three turns from now.

[shooting agent behavior, all other agents just block]
At the time when Weaver catches up with you:
	if Weaver is activated:
		if the player is not in the jetbridge:
			now Weaver is in the location;
			Weaver warns in one turn from now;
			now the initial appearance of Weaver is "Weaver has his gun trained on you!";
			say "Weaver finds you! He points his gun at you! 'Drop to your knees! Get your hands behind your head!'";
			if Clayton is in the location and Clayton is activated:
				say "Clayton turns his head to look at the man. 'Weaver,' he hisses. He turns toward you. 'Fuck that guy! He hasn't got the guts to do anything.'";
		otherwise:
			if the jetdoor is open:
				say "Weaver enters the jetbridge! He yells at you to surrender.";
				now Weaver is in the location;
				Weaver warns in one turn from now;
				now the initial appearance of Weaver is "Weaver has his gun trained on you!";
			otherwise if the jetdoor is unlocked:
				say "Weaver slams the jetbridge door open and points his gun at you! 'Get on your knees!'";
				now Weaver is in the location;
				Weaver warns in one turn from now;
				now the initial appearance of Weaver is "Weaver has his gun trained on you!";
			otherwise:
				say "Weaver slams up against the jetdoor, cursing out loud, trying to get through, but the jetdoor has pressurized and locked down.[line break][line break]You are safe for now.";
				the ORBFLIGHT Cabin opens in zero turns from now.

After going to the ORBFLIGHT Cabin during The Gunfight:
	say "You walk down the jetbridge, into the cabin of the waiting aircraft.".

At the time when the ORBFLIGHT Cabin opens:
	say "You drop the handle to the floor and take a breath.";
	now the description of the jetbridge is "The jetbridge now feels like a portal to another world, the solid slab of the jetdoor an absolute barrier between the near-death of the concourse and your own fragile body. Sounds from the concourse are muffled, far-off, and unable to get to you.
	
	To your south, you see the open door to the ORBFLIGHT aircraft, waiting for you to board.";
	say "A meek voice to your south calls out: 'Hello?";	
	now the ORBFLIGHT Cabin is mapped south of the jetbridge;
	try looking.
	
At the time when Weaver warns:
	if Weaver is activated and Weaver is in the location:	
		say "You hear the hammer of Weaver's gun click back. 'Suspect! Do not try to escape! Get down on your knees and put your hands behind your head! Comply!'";
		if Clayton is in the location and Clayton is activated and the player is carrying the handgun:
			say "'Bullshit!' Clayton yells at Weaver. He turns to you. 'Shoot that son of a bitch.'";
		now the initial appearance of Weaver is "Weaver is going to shoot you! Do something!";
		Weaver shoots in two turns from now.
		

At the time when the SWAT team tries opening the jetdoor:
	say "There is a clatter at the jetdoor, and voices yelling for you to surrender - but they are unable to get the pressurized door open.";
	the ORBFLIGHT Cabin opens in zero turns from now.

[END FOUR - SHOT - SHOT BY TREASURY]		
At the time when Weaver shoots:
	if Weaver is activated and Weaver is in the location and Clayton is in the location and the SWAT team is in the location:
		if Clayton is deactivated:
			say "As Weaver prepares to fire, you see Clayton reach up weakly from the ground and fire his weapon!";
			say "Weaver twists and falls to the floor, groaning in pain.";
		if Clayton is activated:
			now Clayton is deactivated;
			say "As Weaver prepares to fire, Clayton twists his hostage around and shoots at Weaver with his weapon!";
			say "Weaver falls to the ground, bleeding onto the tile.";
			say "The SWAT team opens fire, bringing Clayton and his hostage down in a hail of gunfire.";
			now Clayton is deactivated;
			now the ORBGROUP agent is deactivated;
			now Clayton does not capture anybody;
			now the initial appearance of Clayton is "Clayton lies on the ground, bleeding out.";	
		now the initial appearance of Weaver is "Weaver lies on the ground, not moving.";		
	otherwise if Weaver is activated and Weaver is in the location:
		say "You hear a loud bang and the wind rushes out of your lungs. Another bang and you collapse to the ground.";
		end the game saying "You have been shot to death.";
		



Chapter 3 - Learning More

NEHI Cell is a room.
Johannesburg ORB Tarmac is a room.

[Ending #1 - Player Reaches Plane]
The ORBFLIGHT Scene is a scene. The ORBFLIGHT Scene begins when The Gunfight ends On The Plane.

The ORBGROUP Aircraft is a region. The ORBGROUP Aircraft can be in-boost, in-hook, in-zero, in-retro, or on-ground. It is on-ground.

The ORBFLIGHT Cabin is a room in the ORBGROUP Aircraft. "[if ORBGROUP Aircraft is on-ground]You are seated in a deep leather reclining seat, looking out a fist-sized porthole to the world outside. The ORBFLIGHT cabin is a small, leather and granite-style paneled hemisphere with a handful of similar seats, each one containing a similarly disconnected, quiet traveler. The ceiling of the cabin is a stylized celestial map.[line break]The cabin exits to the outside, east.[otherwise if ORBGROUP Aircraft is in-boost]Your head is crushed into your seat, unable to move - the rest of your body fares no better - you are accelerating faster, harder, than anything you have ever felt before, riding on a single solid chemical booster engine into the upper atmosphere. Your head, fixed in place as it is, fortunately has the porthole to look out at.[otherwise if ORBGROUP Aircraft is in-hook]You can't breathe - you feel two-dimensional. The ORBFLIGHT you are sitting in is being flung into space by a huge slingsteel tether and orbit hook dangling from a low-earth satellite system. You try to remind yourself at some point this will all get better and your lungs will be able to inflate again. The porthole shows a rapidly-changing view. The celestial map above you has changed, too.[otherwise if ORBGROUP Aircraft is in-zero]NO IN-ZERO DESCRIPTION[otherwise if ORBGROUP Aircraft is in-retro]NO IN-RETRO DESCRIPTION[end if]";

The ORBFLIGHT Tailsection is a room in the ORBGROUP Aircraft. It is south of the Cabin. "[if ORBGROUP Aircraft is on-ground]NO ON-GROUND DESCRIPTION[otherwise if ORBGROUP Aircraft is in-boost]NO IN-BOOST DESCRIPTION[otherwise if ORBGROUP Aircraft is in-hook]NO IN-HOOK DESCRIPTION[otherwise if ORBGROUP Aircraft is in-zero]NO IN-ZERO DESCRIPTION[otherwise if ORBGROUP Aircraft is in-retro]NO IN-RETRO DESCRIPTION[end if]".

The ORBFLIGHT Headsection is a room in the ORBGROUP Aircraft. It is north of the Cabin. "[if ORBGROUP Aircraft is on-ground]NO ON-GROUND DESCRIPTION[otherwise if ORBGROUP Aircraft is in-boost]NO IN-BOOST DESCRIPTION[otherwise if ORBGROUP Aircraft is in-hook]NO IN-HOOK DESCRIPTION[otherwise if ORBGROUP Aircraft is in-zero]NO IN-ZERO DESCRIPTION[otherwise if ORBGROUP Aircraft is in-retro]NO IN-RETRO DESCRIPTION[end if]".

[since on-ground is same for takeoff and destination, cant show location. show overlays instead]

The celestial map is a backdrop in the Cabin. "[if ORBGROUP Aircraft is on-ground]NO ON-GROUND DESCRIPTION[otherwise if ORBGROUP Aircraft is in-boost]NO IN-BOOST DESCRIPTION[otherwise if ORBGROUP Aircraft is in-hook]NO IN-HOOK DESCRIPTION[otherwise if ORBGROUP Aircraft is in-zero]NO IN-ZERO DESCRIPTION[otherwise if ORBGROUP Aircraft is in-retro]NO IN-RETRO DESCRIPTION[end if]".

The porthole is a backdrop in the Cabin. "[if ORBGROUP Aircraft is on-ground]NO ON-GROUND DESCRIPTION[otherwise if ORBGROUP Aircraft is in-boost]NO IN-BOOST DESCRIPTION[otherwise if ORBGROUP Aircraft is in-hook]NO IN-HOOK DESCRIPTION[otherwise if ORBGROUP Aircraft is in-zero]NO IN-ZERO DESCRIPTION[otherwise if ORBGROUP Aircraft is in-retro]NO IN-RETRO DESCRIPTION[end if]".

Instead of doing anything other than looking, examining, or waiting:
	if the ORBGROUP Aircraft is in-boost or the ORBGROUP Aircraft is in-hook or the ORBGROUP Aircraft is in-retro:
		[you need to make an allowance for looking at an object! FIX THIS!]
		say "You can't even begin to move the muscles necessary - the G-forces have you absolutely pinned to your seat." instead;
	otherwise:
		continue the action.
		
Before examining something:
	if the ORBGROUP Aircraft is in-boost or the ORBGROUP Aircraft is in-hook or the ORBGROUP Aircraft is in-retro:
		say "With a monumental effort, you manage to move your head to look at [the noun]...";
	continue the action.
	
		
[doing things in zero-g?]

The ORBGROUP flight attendant is a person in the Cabin. "The ORBGROUP flight attendant walks the cabin, making sure things are in order." The description is "WOAH NO DESCRIPTION!".

The harness is a container in the Cabin. It is fixed in place. It is locked. It is closed. "[if closed]A complicated force harness keeps you buckled into your seat.[otherwise]The seat harness lies discarded, hanging from the seat.[end if]". The description is "The harness is a remote-release ORBGROUP-standard five-point acceleration model, designed to keep you safe and secure while the ORBGROUP flight makes initial ascent and final descent.[line break][if closed]The harness is secure around your shoulders, keeping you tight to the seat.[otherwise]The harness lay open and loose around you. You can move about if you wish.[end if][if locked]A light on the buckle glows red, indicating it has been remotely locked.[otherwise]A green light shines from the harness buckle - you can now open or close the harness yourself.[end if]". Understand the commands "unbuckle" and "unclip" as "open". Understand the commands "secure" and "buckle" as "close".

Before going from ORBFLIGHT Cabin:
	if harness is closed:
		say "Your harness is closed - you can't go anywhere until your harness is released." instead;
	otherwise:
		continue the action.

The PAXET display is a thing in the Cabin. The PAXET display is fixed in place. The PAXET display can be either locked or unlocked. It is unlocked. "A PAXET display hinges out from your seat, waiting for a command.". The description is "[if display is unlocked]The PAXET display reads INSERT LOGOSTICK and appears ready to operate.[otherwise]The PAXET display reads GRAVITY LOCKDOWN and does not look like it will accept a logostick.[end if]".

The Zink magazine is a thing in the Cabin. "[if ORBGROUP Aircraft is on-ground]The latest Zink magazine is here.[otherwise if ORBGROUP Aircraft is in-boost]The Zink magazine is here, smeared flat against your chest.[otherwise if ORBGROUP Aircraft is in-hook]The Zink magazine has crawled up your chest and is apparently trying to choke you.[otherwise if ORBGROUP Aircraft is in-zero]The Zink magazine floats effortlessly in the air near your head.[end if]".

Instead of inserting the logostick into the PAXET Display:
	if the display is unlocked:
		say "You slot the logostick into the port in the side of the terminal, and it boots up silently.";
		now the PAXET Trunk is activated;
	otherwise:
		say "You notice that the screen is flashing GRAVITY LOCKDOWN, and that the logostick port is locked closed. You can't use this right now.";

After dropping anything when in the Cabin:
	say "The attendant calmly picks up [the noun] and hands it back to you.";
	now the player is carrying the noun.

When the ORBFLIGHT Scene begins:
	if the player is carrying handgun:
		say "You try, as covertly as you can, to drop the handgun in the jetbridge as you board.";
		remove the handgun from play;
	say "The ORBFLIGHT crew, waiting in the cabin, welcomes you aboard, acting as if they hadn't realized - or were simply kind enough to ignore - that you had run through a monsoon of bullets to get here.";
	say "The attendant shows you to your harness module and makes the usual motions to instruct you on emergency release procedures in the case of zero-atmosphere depressurization, gimbal lock, and solar flares.";
	say "At last, she drops the latest Zink in your lap and smiles, checking that your harness is buckled and that you're ready to head up.";
	the Captain announces skyhook in 12 turns from now;
	the Attendant buckles up in 15 turns from now;
	try looking.
	
at the time when the Attendant buckles up:
	say "The attendant near you smiles, folds down a chair from the cabin wall, and straps in with a harness, getting ready for launch.";
	if PAXET Browsing is happening:
		say "[fixed letter spacing][bold type]***GRAVITATIONAL SAFETY OVERRIDE***[line break]***PLEASE PREPARE FOR TAKEOFF***[roman type][line break][variable letter spacing]";
	now the PAXET Display is locked;
	now the PAXET Trunk is deactivated;
	now the initial appearance of the ORBGROUP flight attendant is "The ORBGROUP flight attendant sits calmly in a jumpseat, reading a magazine.";
	now the description of the ORBGROUP flight attendant is "WOAH NO DESCRIPTION!".
	
at the time when the Captain announces skyhook:
	say "You hear the overhead intercom go on. A curt, edged voice comes on the line.";
	say "'Welcome to ORBGROUP flight TWO-ALPHA-TWO, service from Johannesburg, South Africa to Alexandria, Virginia in the United States. We're uh - just coming off a bit of trouble in the terminal, but that's not our concern at the moment. ORBGROUP has told us that our safest option right now is to hook up and get skyborne. Your ORBGROUP in-can flight officers Trieste and Mercer here, letting you know today's orbital operators are on loan from Conica, who reminds you that space is a journey, not a destination.'";
	say "You can hear quiet conversation in the cockpit. Finally the intercom comes back on.";
	say "'We're expecting to hook up in about ten minutes, at which point we're looking at a five-minute trip up, a fifteen-minute float, and ten minutes burn to land at Alexandria, putting you on the ground at just around two-thirty AM, Eastern time.'";
	say "'Conica is indicating we're eight minutes from first hookup burn, so please stay in those harnesses and we'll get you weightless just as soon as we can. Thanks for coming along with us here at ORBGROUP.'";
	the Skyhook burn starts in six turns from now.
	
at the time when the Skyhook burn starts:
	the Skyhook contacts in five turns from now;
	remove the PAXET Display from play;
	now the ORBGROUP Aircraft is in-boost;
	say "A red light goes on above your head. At the same time, there is a whirring, flanging hum coming from beneath your seat.";
	say "You see the PAXET display retract into your seat as if it was taking cover.";
	say "The intercom clips on:";
	say "'Flight attendants verify clips closed, ground locks disengage. Dry-fuel fuse is lit.'";
	say "It's only then that you realize what an ORBFLIGHT really is, outside of standard travel for the rich and powerful: it's sitting just inches above a mountain of self-oxidizing micro-solar hydrogen fuel.";
	say "'Alexandria in a half-hour, folks. Here we go.'";
	say "[italic type]The fuel ignites.[roman type][line break]";
	try looking;
	
at the time when the Skyhook contacts:
	now the ORBGROUP Aircraft is in-hook;
	the ORBFLIGHT Goes Zero-G in five turns from now;
	say "As you are becoming familiar - almost comfortable - with the forces mashing your head into your seat, you can hear the click of the intercom.";
	say "'Crew stand by for hookup and stage two acceleration.'";
	say "[italic type]Oh, for God's sake - [roman type][line break]";
	say "The celestial map above you changes to a rapidly-draining timer and superimposed icon of a capsule - supposedly the aircraft you are riding in - suspended by a hook that hangs on a phantom line.";
	say "You can see nothing unusual outside, but know that the skyhook is approaching, tethered to a platform in near-earth orbit.";
	say "'Folks, headwinds have put us at a bit of a velocity disadvantage - we're looking at a bit of a bumpy hookup. If you'd do us a favor and keep your mouths open for just the next few seconds, you'll avoid any damage to your teeth on impact.'";
	say "'Five - four -'[line break]";
	say "You open your mouth.";
	say "There is only the very slightest sound of steel whistling through sky, when an impact flattens you against your seat, the steel buckle of your harness stamping its mechanical imprint on your chest. Your jaw slams shut and your teeth crack against one another.";
	say "'Woah,' goes the intercom. The celestial map announces 'HOOKUP'.";
	now the initial appearance of the ORBGROUP flight attendant is "The ORBGROUP flight attendant appears to be asleep.";
	now the description of the ORBGROUP flight attendant is "What a jerk.";
	try looking;
	
at the time when the ORBFLIGHT Goes Zero-G:
	now the ORBGROUP Aircraft is in-zero;
	say "You feel the tension on your chest and neck melt away.";
	say "The sound of the rocket booster has faded as well - you are aware only of an incidental ringing left in your ears.";
	say "Breath returns to your lungs, blood once again flows to your brain. In fact, the whole of you seems to be light, rising, and free.";
	say "But it doesn't stop - you are rising from your seat, bound only by the harness. The magazine perched on your neck begins to float away, the pages flapping a little like wings.";
	say "'On behalf of the first officer, myself, and all our orbital flight crew,' says the intercom, 'Welcome to near-earth orbit and microgravity.'";
	say "[line break][italic type]Terrific.[roman type][line break]";
	now the harness is unlocked;
	say "[line break]With a faint beep, your harness buckle goes from red to green.";
	now the PAXET Display is in the ORBFLIGHT Cabin;
	now the PAXET Display is unlocked;
	say "The PAXET display that had gone underground during the pain and suffering of boost and hookup slowly extends from its hiding place, as if it were truly unaware anything had occurred.";
	say "'Folks, you're free to move about the cabin for a bit as we make the suborbital leg of our journey - the cabin attendants will be sure to get you seated and buckled in when we need to prepare for retro-fire. Enjoy, and thanks for traveling with ORBGROUP.'";
	the ORBFLIGHT Prepares for Retro in 20 turns from now;
	now the initial appearance of the ORBGROUP flight attendant is "The ORBGROUP flight attendant is awake, and watching travelers float about in zero-G.";
	try looking;

	
at the time when the ORBFLIGHT Prepares for Retro:
	the ORBFLIGHT Goes Retro in four turns from now;
	if the player is not in the ORBFLIGHT Cabin:
		say "The attendant helps you back to your seat, BABY!";
		say "You are buckled into your harness.";
		now the player is in the ORBFLIGHT Cabin;
	now the harness is closed;
	now the harness is locked;
	say "You see the harness around you go red.";
	if PAXET Browsing is happening:
		say "[fixed letter spacing][bold type]***GRAVITATIONAL SAFETY OVERRIDE***[line break]***PLEASE PREPARE FOR RETRO BURN***[roman type][line break][variable letter spacing]";
	now the PAXET Display is locked;
	now the PAXET Trunk is deactivated;
	
at the time when the ORBFLIGHT Goes Retro:
	now the ORBGROUP Aircraft is in-retro;
	the ORBFLIGHT Touches Down in five turns from now;
	remove the PAXET Display from play;
	say "The PAXET Display slides back into its holder again.";
	say "RETRO, BABY!";
	now the Zink magazine is in the ORBFLIGHT headsection;
	say "The Zink magazine shoots off down the cabin toward the front section.";
	
at the time when the ORBFLIGHT Touches Down:
	now the ORBGROUP Aircraft is on-ground;
	now the Alexandria ORBGROUP Terminal is mapped east of the ORBFLIGHT Cabin;
	say "LANDED, BABY!";
	now the harness is unlocked;
	say "[line break]With a faint beep, your harness buckle goes from red to green.";
	

[Ending #2 - Player Surrenders]
The NEHI Interrogation is a scene. The NEHI Interrogation begins when The Gunfight ends With Treasury.

[Ending #3 - Player Goes with Clayton]
The ORB Escape is a Scene. The ORB Escape begins when The Gunfight ends With Clayton.



[Universal - PAXET]
PAXET-Region is a Region.

PAXET Trunk is a room in PAXET-Region. The PAXET trunk can be activated or deactivated. The PAXET trunk is deactivated. PAXET Trunk has a number called Current Node. The Current Node is 0.

PAXET Browsing is a Scene. PAXET Browsing begins when the PAXET Trunk is activated. PAXET Browsing ends when the PAXET Trunk is deactivated.
When PAXET Browsing begins:
	The PAXET News Flash Happens in five turns from now.
When PAXET Browsing ends:
	say "[fixed letter spacing][bold type]***PAXET SESSION TERMINATED***[roman type][variable letter spacing][line break]";
	say "You eject your logostick and the terminal blinks off.";
	try looking.

Understand "restart/restore/save/quit" as "[meta-command]".

After reading a command while PAXET Browsing is happening: 
	if the player's command matches "[meta-command]", make no decision; 
	if the player's command matches "EXIT":
		now the PAXET Trunk is deactivated;
		follow the advance time rule;
		follow the timed events rule;
		consider the scene changing rules;
		rule succeeds;
	if the player's command matches "HELP":
		say "[fixed letter spacing]WOAH NEED TO PUT HELP TEXT HERE![variable letter spacing]";
		follow the advance time rule;
		follow the timed events rule;
		consider the scene changing rules;
		rule succeeds;
	if the player's command matches "STOCKS":
		say "[fixed letter spacing]***ACCESS TRADE ACCOUNT***[line break]";
		say "[bold type]***PAXET AUXILIARY SECURITY***[roman type][line break]***PLEASE PLACE RIGHT EYE AGAINST DISPLAY FOR RETINAL SCAN***[variable letter spacing][line break]";
		say "You now realize one good reason why someone may have wanted your eye. And now that they have it, you are suddenly worried about the company and fortune you have waiting for you when you get back to the City of Industry.";
		say "But for now, there doesn't look to be much you can do about it.";
		follow the advance time rule;
		consider the scene changing rules;
		rule succeeds;
	say line break; 
	repeat through Table of PAXET Trunk Options: 
		if the player's command includes topic entry:
			now the Current Node of PAXET Trunk is the node entry;
			follow the advance time rule;
			follow the timed events rule; 
			consider the scene changing rules;
			rule succeeds; 
	say "[fixed letter spacing][bold type]UNKNOWN COMMAND - TYPE HELP FOR COMMAND LIST.[roman type][variable letter spacing][line break]"; 
	follow the advance time rule; 
	follow the timed events rule;
	consider the scene changing rules;
	rule succeeds.
	

Table of PAXET Trunk Options
topic	topicfriendly	node	specialtable	description
"NEWS"	"NEWS"	1	Table of PAXET News Options	"Check latest news personalized for You, Inc."
"STOCKS"	"STOCKS"	2	Table of PAXET Stock Options	"Control your currently held stocks."
"MESSAGES"	"MESSAGES"	3	Table of PAXET Message Options	"Scan incoming messages"
"HELP"	"HELP"	4	--	"Get help with PAXET commands"
"EXIT"	"EXIT"	5	--	"Exit PAXET and eject Logostick"

Table of PAXET News Options
node	description	articlevisible
1	"News Item 1"	true
2	"News Item 2"	false

Table of PAXET Stock Options
node	description	articlevisible
1	"Stock Item 1"	true
2	"Stock Item 2"	false

Table of PAXET Message Options
node	description	articlevisible
1	"Message Item 1"	true
2	"Message Item 2"	false

Before reading a command while PAXET Browsing is happening: 
	say "[fixed letter spacing]PAXET Trunk - Welcome, You Inc.[line break]";
	say "TABLE OF MAIN OPTIONS[line break][line break]";
	repeat through Table of PAXET Trunk Options:
		say "[topicfriendly entry] - [description entry][line break]";
	if Current Node of PAXET Trunk is not 0:
		let the Current NodeName be the topicfriendly corresponding to a node of the Current Node of PAXET Trunk in the Table of PAXET Trunk Options;
		say "--------------------[line break]";
		say "TABLE OF [Current NodeName] OPTIONS[line break]";
		repeat through specialtable corresponding to a node of the Current Node of PAXET Trunk in the Table of PAXET Trunk Options:
			if articlevisible entry is true, say "[node entry] - [description entry][line break]";
	say "[variable letter spacing][line break]";

At the time when The PAXET News Flash Happens:
	say "PAXET baby!";
	[do stock calculations regardless of scene]
	[do add news item regardless of scene]
	[show one liner on paxet if browsing]
			

Part 2 - The Lens

Chapter 4 - Alexandria Day One

Alexandria Day One is a Scene. Alexandria Day One begins when the player is in Alexandria.

When Alexandria Day One begins:
	cycle Argot Schemes.

Alexandria is a Region.
Alexandria ORBGROUP Terminal is a room in Alexandria.

Haven is a Region in Alexandria.

Haven Entrance is a room in Haven.
Haven Loft is a room in Haven. It is above Haven Entrance.
Haven Unit Hallway North is a room in Haven. It is north of Haven Loft.
Haven Unit Hallway South is a room in Haven. It is south of Haven Loft.
Haven Unit Four is a room in Haven. It is east of Haven Unit Hallway South.
Haven Backroom is a room in Haven.

Argot is a Region in Alexandria.


Table of Argot Data Schemes
number	short	long
1	"crucifixion"	"a matchwork pattern of Christ-like figures"
2	"currency"	"a blob of pyramidial currency chits"
3	"stylometry"	"a series of numerical buckets receiving individual words from a floating document"
4	"steganography"	"a matrix of melted wax droplets revealing a message within the pixels of an image"
5	"japanese fashion"	"a Japanese noh-style wakizure gliding down a fashion runway, with a bleeding left eye"
6	"grain price"	"a graphical breakout chart of grain sales radiating from major food centers"
7	"electromagnetic warfare"	"a"
8	"antigravity"	"a"
9	"heliography"	"a"
10	"norse mythos"	"a"
11	"heraldry"	"a"
12	"satellite linkage"	"a"
13	"tectonics"	"a"
14	"bird migration"	"a"
15	"classical leitmotif"	"a"
16	"namboku line"	"a"
17	"winter climate"	"a"
18	"spring climate"	"a"
19	"coriolis effect"	"a"
20	"judicial opinion"	"a"
21	"artillery grid"	"a"
22	"plasmodium stain"	"a"
23	"competitive chess"	"a"
24	"biathlon"	"a"
25	"signal bleed"	"a"

Table of Argot Data Join Nouns
number	noun
1	"diorama"
2	"triptych"
3	"corpus"
4	"hologram"
5	"mimeograph"

Table of Argot Data Join Verbs
number	verb
1	"overlaid on"
2	"joined with"
3	"enmeshed in"
4	"rotating around"


Argot Lounge is a room in Argot. The description is "Blah blah. The wall is displaying a [the short corresponding to a number of primary scheme number of Data Wall in the Table of Argot Data Schemes]-[the short corresponding to a number of secondary scheme number of Data Wall in the Table of Argot Data Schemes] [the noun corresponding to a number of noun join number of Data Wall in the Table of Argot Data Join Nouns], the ceiling above you is displaying a [the short corresponding to a number of primary scheme number of Data Ceiling in the Table of Argot Data Schemes]-[the short corresponding to a number of secondary scheme number of Data Ceiling in the Table of Argot Data Schemes] [the noun corresponding to a number of noun join number of Data Ceiling in the Table of Argot Data Join Nouns], and the floor at your feet is displaying a [the short corresponding to a number of primary scheme number of Data Floor in the Table of Argot Data Schemes]-[the short corresponding to a number of secondary scheme number of Data Floor in the Table of Argot Data Schemes] [the noun corresponding to a number of noun join number of Data Floor in the Table of Argot Data Join Nouns].".

The Data Wall is a backdrop in Argot Lounge. The description is "The surface of the wall contains [the long corresponding to a number of primary scheme number of Data Wall in the Table of Argot Data Schemes], [the verb corresponding to a number of verb join number of Data Wall in the Table of Argot Data Join Verbs] [the long corresponding to a number of secondary scheme number of Data Wall in the Table of Argot Data Schemes].". The Data Wall has a number called primary scheme number. The primary scheme number is 1. The Data Wall has a number called secondary scheme number. The secondary scheme number is 1. The Data Wall has a number called primary scheme number. The primary scheme number is 1. The Data Wall has a number called secondary scheme number. The secondary scheme number is 1. The Data Wall has a number called noun join number. The noun join number is 1. The Data Wall has a number called verb join number. The verb join number is 1.
The Data Floor is a backdrop in Argot Lounge. The description is "The floor is a holographic image of [the long corresponding to a number of primary scheme number of Data Floor in the Table of Argot Data Schemes], [the verb corresponding to a number of verb join number of Data Floor in the Table of Argot Data Join Verbs] [the long corresponding to a number of secondary scheme number of Data Floor in the Table of Argot Data Schemes].". The Data Floor has a number called scheme number. The scheme number is 1.  The Data Floor has a number called secondary scheme number. The secondary scheme number is 1. The Data Floor has a number called primary scheme number. The primary scheme number is 1. The Data Floor has a number called secondary scheme number. The secondary scheme number is 1. The Data Floor has a number called noun join number. The noun join number is 1. The Data Floor has a number called verb join number. The verb join number is 1.
The Data Ceiling is a backdrop in Argot Lounge. The description is "Looking up at the ceiling, you see [the long corresponding to a number of primary scheme number of Data Ceiling in the Table of Argot Data Schemes], [the verb corresponding to a number of verb join number of Data Ceiling in the Table of Argot Data Join Verbs] [the long corresponding to a number of secondary scheme number of Data Ceiling in the Table of Argot Data Schemes].". The Data Ceiling has a number called scheme number. The scheme number is 1.  The Data Ceiling has a number called secondary scheme number. The secondary scheme number is 1. The Data Ceiling has a number called primary scheme number. The primary scheme number is 1. The Data Ceiling has a number called secondary scheme number. The secondary scheme number is 1. The Data Ceiling has a number called noun join number. The noun join number is 1. The Data Ceiling has a number called verb join number. The verb join number is 1.

Understand "change it" or "say change it" or "yell change it" or "shout change it" as changing schemes.
Changing schemes is an action applying to nothing.
Check an actor changing schemes: 
	if the actor is not in Argot:
		if the actor is the player:
			say "Nothing happens." instead;
		stop the action.
		
Carry out an actor changing schemes:
	if the actor is the player:
		say "As if by magic, the patterns around the room change to your command.";
	otherwise:
		say "As you hear the words, the patterns around you change.";
	cycle Argot Schemes.

To cycle Argot Schemes:
	choose a random row in the Table of Argot Data Schemes; 
	now the primary scheme number of Data Wall is number entry;
	choose a random row in the Table of Argot Data Schemes; 
	now the secondary scheme number of Data Wall is number entry;
	choose a random row in the Table of Argot Data Join Nouns; 
	now the noun join number of Data Wall is number entry;
	choose a random row in the Table of Argot Data Join Verbs; 
	now the verb join number of Data Wall is number entry;[end]
	choose a random row in the Table of Argot Data Schemes; 
	now the primary scheme number of Data Floor is number entry;
	choose a random row in the Table of Argot Data Schemes; 
	now the secondary scheme number of Data Floor is number entry;
	choose a random row in the Table of Argot Data Join Nouns; 
	now the noun join number of Data Floor is number entry;
	choose a random row in the Table of Argot Data Join Verbs; 
	now the verb join number of Data Floor is number entry;[end]
	choose a random row in the Table of Argot Data Schemes; 
	now the primary scheme number of Data Ceiling is number entry;
	choose a random row in the Table of Argot Data Schemes; 
	now the secondary scheme number of Data Ceiling is number entry;
	choose a random row in the Table of Argot Data Join Nouns; 
	now the noun join number of Data Ceiling is number entry;
	choose a random row in the Table of Argot Data Join Verbs; 
	now the verb join number of Data Ceiling is number entry;

Argot Backroom is a room in Argot.